/-
Copyright (c) 2024 RÃ©my Degenne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: RÃ©my Degenne, Lorenzo Luccioli
-/
import TestingLowerBounds.ForMathlib.MaxMinEqAbs
import TestingLowerBounds.Testing.Risk
import TestingLowerBounds.Testing.BoolMeasure
import Mathlib.MeasureTheory.Order.Group.Lattice

/-!
# Simple Bayesian binary hypothesis testing

## Main definitions

* `simpleBinaryHypTest`

## Main statements

* `fooBar_unique`

## Notation

## Implementation details

-/

open MeasureTheory

open scoped ENNReal NNReal

namespace ProbabilityTheory

variable {Î˜ ğ’³ ğ’³' ğ’´ ğ’µ : Type*} {mÎ˜ : MeasurableSpace Î˜} {mğ’³ : MeasurableSpace ğ’³}
  {mğ’³' : MeasurableSpace ğ’³'} {mğ’´ : MeasurableSpace ğ’´} {mğ’µ : MeasurableSpace ğ’µ}
  {Î¼ Î½ : Measure ğ’³} {p : â„â‰¥0âˆ}

section TwoHypKernel

/-- The kernel that sends `false` to `Î¼` and `true` to `Î½`. -/
def twoHypKernel (Î¼ Î½ : Measure ğ’³) : Kernel Bool ğ’³ where
  toFun := fun b â†¦ bif b then Î½ else Î¼
  measurable' := measurable_discrete _

@[simp] lemma twoHypKernel_true : twoHypKernel Î¼ Î½ true = Î½ := rfl

@[simp] lemma twoHypKernel_false : twoHypKernel Î¼ Î½ false = Î¼ := rfl

@[simp] lemma twoHypKernel_apply (b : Bool) : twoHypKernel Î¼ Î½ b = bif b then Î½ else Î¼ := rfl

instance [IsFiniteMeasure Î¼] [IsFiniteMeasure Î½] : IsFiniteKernel (twoHypKernel Î¼ Î½) :=
  âŸ¨max (Î¼ Set.univ) (Î½ Set.univ), max_lt (measure_lt_top _ _) (measure_lt_top _ _),
    fun b â†¦ by cases b <;> simpâŸ©

instance [IsProbabilityMeasure Î¼] [IsProbabilityMeasure Î½] :
    IsMarkovKernel (twoHypKernel Î¼ Î½) := by
  constructor
  intro b
  cases b
  Â· simp only [twoHypKernel_apply, cond_false]
    infer_instance
  Â· simp only [twoHypKernel_apply, cond_true]
    infer_instance

lemma Kernel_bool_eq_twoHypKernel (Îº : Kernel Bool ğ’³) :
    Îº = twoHypKernel (Îº false) (Îº true) := by
  ext (_ | _) <;> simp

@[simp]
lemma comp_twoHypKernel (Îº : Kernel ğ’³ ğ’´) :
    Îº âˆ˜â‚– (twoHypKernel Î¼ Î½) = twoHypKernel (Îº âˆ˜â‚˜ Î¼) (Îº âˆ˜â‚˜ Î½) := by
  ext b : 1
  rw [Kernel.comp_apply]
  cases b with
  | false => simp
  | true => simp

lemma measure_comp_twoHypKernel (Î¼ Î½ : Measure ğ’³) (Ï€ : Measure Bool) :
    twoHypKernel Î¼ Î½ âˆ˜â‚˜ Ï€ = Ï€ {true} â€¢ Î½ + Ï€ {false} â€¢ Î¼ := by
  ext s hs
  rw [Measure.bind_apply hs (Kernel.measurable _)]
  simp only [twoHypKernel_apply, lintegral_fintype, Fintype.univ_bool, Finset.mem_singleton,
    Bool.true_eq_false, not_false_eq_true, Finset.sum_insert, cond_true, Finset.sum_singleton,
    cond_false, Measure.coe_add, Measure.coe_smul, Pi.add_apply, Pi.smul_apply, smul_eq_mul]
  congr 1 <;> rw [mul_comm]

lemma absolutelyContinuous_measure_comp_twoHypKernel_left (Î¼ Î½ : Measure ğ’³)
    {Ï€ : Measure Bool} (hÏ€ : Ï€ {false} â‰  0) :
    Î¼ â‰ª twoHypKernel Î¼ Î½ âˆ˜â‚˜ Ï€ :=
  measure_comp_twoHypKernel _ _ _ â–¸ add_comm _ (Ï€ {true} â€¢ Î½) â–¸
    (Measure.absolutelyContinuous_smul hÏ€).add_right _

lemma absolutelyContinuous_measure_comp_twoHypKernel_right (Î¼ Î½ : Measure ğ’³)
    {Ï€ : Measure Bool} (hÏ€ : Ï€ {true} â‰  0) :
    Î½ â‰ª twoHypKernel Î¼ Î½ âˆ˜â‚˜ Ï€ :=
  measure_comp_twoHypKernel _ _ _ â–¸ (Measure.absolutelyContinuous_smul hÏ€).add_right _

lemma sum_smul_rnDeriv_twoHypKernel (Î¼ Î½ : Measure ğ’³) [IsFiniteMeasure Î¼] [IsFiniteMeasure Î½]
    (Ï€ : Measure Bool) [IsFiniteMeasure Ï€] :
    (Ï€ {true} â€¢ Î½.rnDeriv (twoHypKernel Î¼ Î½ âˆ˜â‚˜ Ï€) + Ï€ {false} â€¢ (Î¼.rnDeriv (twoHypKernel Î¼ Î½ âˆ˜â‚˜ Ï€)))
      =áµ[twoHypKernel Î¼ Î½ âˆ˜â‚˜ Ï€] 1 := by
  have h1 := Measure.rnDeriv_smul_left_of_ne_top Î½ (twoHypKernel Î¼ Î½ âˆ˜â‚˜ Ï€)
    (measure_ne_top Ï€ {true})
  have h2 := Measure.rnDeriv_smul_left_of_ne_top Î¼ (twoHypKernel Î¼ Î½ âˆ˜â‚˜ Ï€)
    (measure_ne_top Ï€ {false})
  have : IsFiniteMeasure (Ï€ {true} â€¢ Î½) := Î½.smul_finite (measure_ne_top _ _)
  have : IsFiniteMeasure (Ï€ {false} â€¢ Î¼) := Î¼.smul_finite (measure_ne_top _ _)
  have h3 := Measure.rnDeriv_add (Ï€ {true} â€¢ Î½) (Ï€ {false} â€¢ Î¼) (twoHypKernel Î¼ Î½ âˆ˜â‚˜ Ï€)
  have h4 := Measure.rnDeriv_self (twoHypKernel Î¼ Î½ âˆ˜â‚˜ Ï€)
  filter_upwards [h1, h2, h3, h4] with a h1 h2 h3 h4
  simp only [Pi.add_apply, Pi.smul_apply, smul_eq_mul, Pi.one_apply] at h1 h2 h3 h4 âŠ¢
  rw [â† h1, â† h2, â† h3, â† measure_comp_twoHypKernel, h4]

lemma sum_smul_rnDeriv_twoHypKernel' (Î¼ Î½ : Measure ğ’³) [IsFiniteMeasure Î¼] [IsFiniteMeasure Î½]
    (Ï€ : Measure Bool) [IsFiniteMeasure Ï€] :
    âˆ€áµ x âˆ‚(twoHypKernel Î¼ Î½ âˆ˜â‚˜ Ï€), Ï€ {true} * Î½.rnDeriv (twoHypKernel Î¼ Î½ âˆ˜â‚˜ Ï€) x
      + Ï€ {false} * Î¼.rnDeriv (twoHypKernel Î¼ Î½ âˆ˜â‚˜ Ï€) x = 1 := by
  filter_upwards [sum_smul_rnDeriv_twoHypKernel Î¼ Î½ Ï€] with x hx
  simpa using hx

/-- The kernel from `ğ’³` to `Bool` defined by
`x â†¦ (Ï€â‚€ * âˆ‚Î¼/âˆ‚(twoHypKernel Î¼ Î½ âˆ˜â‚˜ Ï€) x + Ï€â‚ * âˆ‚Î½/âˆ‚(twoHypKernel Î¼ Î½ âˆ˜â‚˜ Ï€) x)`.
It is the Bayesian inverse of `twoHypKernel Î¼ Î½` with respect to `Ï€`
(see `bayesInv_twoHypKernel`). -/
noncomputable
def twoHypKernelInv (Î¼ Î½ : Measure ğ’³) (Ï€ : Measure Bool) : Kernel ğ’³ Bool where
  toFun x :=
    if Ï€ {true} * Î½.rnDeriv (twoHypKernel Î¼ Î½ âˆ˜â‚˜ Ï€) x
      + Ï€ {false} * Î¼.rnDeriv (twoHypKernel Î¼ Î½ âˆ˜â‚˜ Ï€) x = 1
    then (Ï€ {true} * Î½.rnDeriv (twoHypKernel Î¼ Î½ âˆ˜â‚˜ Ï€) x) â€¢ Measure.dirac true
      + (Ï€ {false} * Î¼.rnDeriv (twoHypKernel Î¼ Î½ âˆ˜â‚˜ Ï€) x) â€¢ Measure.dirac false
    else Measure.dirac true
  measurable' := by
    refine Measurable.ite ?_ ?_ measurable_const
    Â· refine measurableSet_preimage ?_ (measurableSet_singleton _)
      exact ((Measure.measurable_rnDeriv _ _).const_mul _).add
        ((Measure.measurable_rnDeriv _ _).const_mul _)
    refine Measure.measurable_of_measurable_coe _ (fun s _ â†¦ ?_)
    simp only [Measure.coe_add, Measure.coe_smul, Pi.add_apply, Pi.smul_apply,
      MeasurableSpace.measurableSet_top, Measure.dirac_apply', smul_eq_mul]
    exact ((measurable_const.mul (Measure.measurable_rnDeriv _ _)).mul measurable_const).add
      ((measurable_const.mul (Measure.measurable_rnDeriv _ _)).mul measurable_const)

lemma twoHypKernelInv_apply (Î¼ Î½ : Measure ğ’³) (Ï€ : Measure Bool) (x : ğ’³) :
    twoHypKernelInv Î¼ Î½ Ï€ x
      = if Ï€ {true} * Î½.rnDeriv (twoHypKernel Î¼ Î½ âˆ˜â‚˜ Ï€) x
          + Ï€ {false} * Î¼.rnDeriv (twoHypKernel Î¼ Î½ âˆ˜â‚˜ Ï€) x = 1
        then (Ï€ {true} * Î½.rnDeriv (twoHypKernel Î¼ Î½ âˆ˜â‚˜ Ï€) x) â€¢ Measure.dirac true
          + (Ï€ {false} * Î¼.rnDeriv (twoHypKernel Î¼ Î½ âˆ˜â‚˜ Ï€) x) â€¢ Measure.dirac false
        else Measure.dirac true := rfl

lemma twoHypKernelInv_apply_ae (Î¼ Î½ : Measure ğ’³) [IsFiniteMeasure Î¼] [IsFiniteMeasure Î½]
    (Ï€ : Measure Bool) [IsFiniteMeasure Ï€] :
    âˆ€áµ x âˆ‚(twoHypKernel Î¼ Î½ âˆ˜â‚˜ Ï€), twoHypKernelInv Î¼ Î½ Ï€ x
      = (Ï€ {true} * Î½.rnDeriv (twoHypKernel Î¼ Î½ âˆ˜â‚˜ Ï€) x) â€¢ Measure.dirac true
        + (Ï€ {false} * Î¼.rnDeriv (twoHypKernel Î¼ Î½ âˆ˜â‚˜ Ï€) x) â€¢ Measure.dirac false := by
  filter_upwards [sum_smul_rnDeriv_twoHypKernel' Î¼ Î½ Ï€] with x hx
  rw [twoHypKernelInv_apply, if_pos hx]

lemma twoHypKernelInv_apply' (Î¼ Î½ : Measure ğ’³) [IsFiniteMeasure Î¼] [IsFiniteMeasure Î½]
    (Ï€ : Measure Bool) [IsFiniteMeasure Ï€] (s : Set Bool) :
    âˆ€áµ x âˆ‚(twoHypKernel Î¼ Î½ âˆ˜â‚˜ Ï€), twoHypKernelInv Î¼ Î½ Ï€ x s
      = Ï€ {true} * Î½.rnDeriv (twoHypKernel Î¼ Î½ âˆ˜â‚˜ Ï€) x * s.indicator 1 true
        + Ï€ {false} * Î¼.rnDeriv (twoHypKernel Î¼ Î½ âˆ˜â‚˜ Ï€) x * s.indicator 1 false := by
  filter_upwards [twoHypKernelInv_apply_ae Î¼ Î½ Ï€] with x hx
  rw [hx]
  simp

lemma twoHypKernelInv_apply_false (Î¼ Î½ : Measure ğ’³) [IsFiniteMeasure Î¼] [IsFiniteMeasure Î½]
    (Ï€ : Measure Bool) [IsFiniteMeasure Ï€] :
    âˆ€áµ x âˆ‚(twoHypKernel Î¼ Î½ âˆ˜â‚˜ Ï€),
      twoHypKernelInv Î¼ Î½ Ï€ x {false} =  Ï€ {false} * Î¼.rnDeriv (twoHypKernel Î¼ Î½ âˆ˜â‚˜ Ï€) x := by
  filter_upwards [twoHypKernelInv_apply_ae Î¼ Î½ Ï€] with x hx
  simp [hx]

lemma twoHypKernelInv_apply_true (Î¼ Î½ : Measure ğ’³) [IsFiniteMeasure Î¼] [IsFiniteMeasure Î½]
    (Ï€ : Measure Bool) [IsFiniteMeasure Ï€] :
    âˆ€áµ x âˆ‚(twoHypKernel Î¼ Î½ âˆ˜â‚˜ Ï€),
      twoHypKernelInv Î¼ Î½ Ï€ x {true} = Ï€ {true} * Î½.rnDeriv (twoHypKernel Î¼ Î½ âˆ˜â‚˜ Ï€) x := by
  filter_upwards [twoHypKernelInv_apply_ae Î¼ Î½ Ï€] with x hx
  simp [hx]

instance (Ï€ : Measure Bool) : IsMarkovKernel (twoHypKernelInv Î¼ Î½ Ï€) := by
  constructor
  intro x
  rw [twoHypKernelInv_apply]
  split_ifs with h
  Â· constructor
    simp [h]
  Â· infer_instance

/- The finiteness hypothesis for Î¼ should not be needed, but otherwise I dont know how to handle
the 3rd case, where I have the complement.
We still need some hp, the right one is probably SigmaFinite. For SFinite ones there is
a counterexample, see the comment above `Measure.prod_eq`.

TODO: generalize this lemma to SigmaFinite measures, there are 2 ways to do it,
one is to try and generalize this proof (for this it may be useful to try and apply the lemma used
in the proof of `Measure.prod_eq`), the other is to use this as an auxiliary lemma and prove
the result for SigmaFinite measures using this one (we can restrict the mesaure to the set
where it is finite and then use this lemma).
I'm not sure which way is better.
-/
lemma measure_prod_ext {Î¼ Î½ : Measure (ğ’³ Ã— ğ’´)} [IsFiniteMeasure Î¼]
    (h : âˆ€ (A : Set ğ’³) (_ : MeasurableSet A) (B : Set ğ’´) (_ : MeasurableSet B),
      Î¼ (A Ã—Ë¢ B) = Î½ (A Ã—Ë¢ B)) :
    Î¼ = Î½ := by
  ext s hs
  apply MeasurableSpace.induction_on_inter generateFrom_prod.symm isPiSystem_prod _ _ _ _ hs
  Â· simp_rw [measure_empty]
  Â· exact fun t âŸ¨A, hA, B, hB, htâŸ© â†¦ ht â–¸ h A hA B hB
  Â· intro t ht h_eq
    rw [measure_compl ht (measure_ne_top Î¼ t), measure_compl ht (h_eq â–¸ measure_ne_top Î¼ t), h_eq,
      â† Set.univ_prod_univ, â† h Set.univ MeasurableSet.univ Set.univ MeasurableSet.univ]
  Â· intro A h_disj h_meas h_eq
    simp_rw [measure_iUnion h_disj h_meas, h_eq]

lemma bayesInv_twoHypKernel (Î¼ Î½ : Measure ğ’³) [IsFiniteMeasure Î¼] [IsFiniteMeasure Î½]
    (Ï€ : Measure Bool) [IsFiniteMeasure Ï€] :
    ((twoHypKernel Î¼ Î½)â€ Ï€) =áµ[twoHypKernel Î¼ Î½ âˆ˜â‚˜ Ï€] twoHypKernelInv Î¼ Î½ Ï€ := by
  symm
  refine eq_bayesInv_of_compProd_eq _ (measure_prod_ext fun A hA B hB â†¦ ?_)
  obtain (rfl | rfl | rfl | rfl) := Bool.cases_set_bool B
  Â· simp
  Â· rw [Measure.compProd_apply_prod hA hB, Measure.map_apply measurable_swap (hA.prod hB),
      Set.preimage_swap_prod, Measure.compProd_apply_prod hB hA, lintegral_singleton,
      twoHypKernel_apply, cond_true, setLIntegral_congr_fun hA _]
    rotate_left
    Â· exact fun x â†¦ Ï€ {true} * (âˆ‚Î½/âˆ‚twoHypKernel Î¼ Î½ âˆ˜â‚˜ Ï€) x
    Â· filter_upwards [twoHypKernelInv_apply' Î¼ Î½ Ï€ {true}] with x hx
      simp [hx]
    simp_rw [mul_comm (Ï€ {true})]
    by_cases h_zero : Ï€ {true} = 0
    Â· simp [h_zero]
    rw [setLIntegral_rnDeriv_mul (absolutelyContinuous_measure_comp_twoHypKernel_right Î¼ Î½ h_zero)
      aemeasurable_const hA]
    simp [mul_comm]
  Â· rw [Measure.compProd_apply_prod hA hB, Measure.map_apply measurable_swap (hA.prod hB),
      Set.preimage_swap_prod, Measure.compProd_apply_prod hB hA, lintegral_singleton,
      twoHypKernel_apply, cond_false, setLIntegral_congr_fun hA _]
    rotate_left
    Â· exact fun x â†¦ Ï€ {false} * (âˆ‚Î¼/âˆ‚twoHypKernel Î¼ Î½ âˆ˜â‚˜ Ï€) x
    Â· filter_upwards [twoHypKernelInv_apply' Î¼ Î½ Ï€ {false}] with x hx
      simp [hx]
    simp_rw [mul_comm (Ï€ {false})]
    by_cases h_zero : Ï€ {false} = 0
    Â· simp [h_zero]
    rw [setLIntegral_rnDeriv_mul (absolutelyContinuous_measure_comp_twoHypKernel_left Î¼ Î½ h_zero)
      aemeasurable_const hA]
    simp [mul_comm]
  Â· rw [Measure.compProd_apply_prod hA hB, Measure.map_apply measurable_swap (hA.prod hB),
      Set.preimage_swap_prod, Measure.compProd_apply_prod hB hA,
      Bool.lintegral_bool, twoHypKernel_apply, twoHypKernel_apply, cond_false, cond_true,
      Set.pair_comm, â† Bool.univ_eq]
    simp only [measure_univ, lintegral_const, MeasurableSet.univ, Measure.restrict_apply,
      Set.univ_inter, one_mul, Measure.restrict_univ]
    rw [Measure.bind_apply hA (by exact fun _ _ â†¦ hB), Bool.lintegral_bool]
    simp

lemma bayesRiskPrior_eq_of_hasGenBayesEstimator_binary (E : estimationProblem Bool Bool Bool)
    (P : Kernel Bool ğ’³) [IsFiniteKernel P] (Ï€ : Measure Bool) [IsFiniteMeasure Ï€]
    [h : HasGenBayesEstimator E P Ï€] :
    bayesRiskPrior E P Ï€
      = âˆ«â» x, â¨… z, Ï€ {true} * (P true).rnDeriv (P âˆ˜â‚˜ Ï€) x * E.â„“ (E.y true, z)
        + Ï€ {false} * (P false).rnDeriv (P âˆ˜â‚˜ Ï€) x * E.â„“ (E.y false, z) âˆ‚(P âˆ˜â‚˜ Ï€) := by
  simp_rw [bayesRiskPrior_eq_of_hasGenBayesEstimator]
  have h1 := bayesInv_twoHypKernel (P false) (P true) Ï€
  have h2 : P = twoHypKernel (P false) (P true) := Kernel_bool_eq_twoHypKernel P
  have h3 : (Pâ€ Ï€) = twoHypKernel (P false) (P true)â€ Ï€ := by congr
  nth_rw 1 [h2]
  nth_rw 4 [h2]
  simp_rw [h3]
  apply lintegral_congr_ae
  filter_upwards [h1, twoHypKernelInv_apply_false (P false) (P true) Ï€,
    twoHypKernelInv_apply_true (P false) (P true) Ï€] with x hx h_false h_true
  congr with z
  rw [Bool.lintegral_bool, hx, h_false, h_true, â† h2]
  ring_nf

end TwoHypKernel

section SimpleBinaryHypTest

/-- Simple binary hypothesis testing problem: a testing problem where `Î˜ = ğ’´ = ğ’µ = {0,1}`, `y` is
the identity and the loss is `â„“(yâ‚€, z) = ğ•€{yâ‚€ â‰  z}`. -/
@[simps]
noncomputable
def simpleBinaryHypTest : estimationProblem Bool Bool Bool where
  y := id
  y_meas := measurable_id
  â„“ := fun (y, z) â†¦ if y = z then 0 else 1
  â„“_meas := measurable_discrete _

@[simp]
lemma risk_simpleBinaryHypTest_true (Î¼ Î½ : Measure ğ’³) (Îº : Kernel ğ’³ Bool) :
    risk simpleBinaryHypTest (twoHypKernel Î¼ Î½) Îº true = (Îº âˆ˜â‚˜ Î½) {false} := by
  simp only [risk, simpleBinaryHypTest, comp_twoHypKernel, twoHypKernel_apply, cond_true, id_eq,
    Bool.true_eq, MeasurableSpace.measurableSet_top]
  calc âˆ«â» z, if z = true then 0 else 1 âˆ‚(Îº âˆ˜â‚˜ Î½)
  _ = âˆ«â» z, Set.indicator {false} (fun _ â†¦ 1) z âˆ‚(Îº âˆ˜â‚˜ Î½) := by
    congr with z
    rw [Set.indicator_apply]
    classical
    simp only [Set.mem_singleton_iff]
    split_ifs with h1 h2 h2
    Â· exact absurd (h2.symm.trans h1) Bool.false_ne_true
    Â· rfl
    Â· rfl
    Â· simp at h1 h2
      exact absurd (h1.symm.trans h2) Bool.false_ne_true
  _ = (Îº âˆ˜â‚˜ Î½) {false} := lintegral_indicator_one (measurableSet_singleton _)

@[simp]
lemma risk_simpleBinaryHypTest_false (Î¼ Î½ : Measure ğ’³) (Îº : Kernel ğ’³ Bool) :
    risk simpleBinaryHypTest (twoHypKernel Î¼ Î½) Îº false = (Îº âˆ˜â‚˜ Î¼) {true} := by
  simp only [risk, simpleBinaryHypTest, comp_twoHypKernel, twoHypKernel_apply, cond_false, id_eq,
    Bool.false_eq, MeasurableSpace.measurableSet_top]
  calc âˆ«â» z, if z = false then 0 else 1 âˆ‚(Îº âˆ˜â‚˜ Î¼)
  _ = âˆ«â» z, Set.indicator {true} (fun _ â†¦ 1) z âˆ‚(Îº âˆ˜â‚˜ Î¼) := by
    congr with z
    rw [Set.indicator_apply]
    classical
    simp only [Set.mem_singleton_iff]
    split_ifs with h1 h2 h2
    Â· exact absurd (h1.symm.trans h2) Bool.false_ne_true
    Â· rfl
    Â· rfl
    Â· simp at h1 h2
      exact absurd (h2.symm.trans h1) Bool.false_ne_true
  _ = (Îº âˆ˜â‚˜ Î¼) {true} := lintegral_indicator_one (measurableSet_singleton _)

/-- The function `x â†¦ ğ•€{Ï€â‚€ * âˆ‚Î¼/âˆ‚(twoHypKernel Î¼ Î½ âˆ˜â‚˜ Ï€) x â‰¤ Ï€â‚ * âˆ‚Î½/âˆ‚(twoHypKernel Î¼ Î½ âˆ˜â‚˜ Ï€) x}`.
It is a Generalized Bayes estimator for the simple binary hypothesis testing problem. -/
noncomputable
def binaryGenBayesEstimator (Î¼ Î½ : Measure ğ’³) (Ï€ : Measure Bool) : ğ’³ â†’ Bool :=
  let E : Set ğ’³ := {x | Ï€ {false} * Î¼.rnDeriv (twoHypKernel Î¼ Î½ âˆ˜â‚˜ Ï€) x
    â‰¤ Ï€ {true} * Î½.rnDeriv (twoHypKernel Î¼ Î½ âˆ˜â‚˜ Ï€) x}
  fun x â†¦ Bool.ofNat (E.indicator 1 x)

lemma binaryGenBayesEstimator_isGenBayesEstimator (Î¼ Î½ : Measure ğ’³) [IsFiniteMeasure Î¼]
    [IsFiniteMeasure Î½] (Ï€ : Measure Bool) [IsFiniteMeasure Ï€] :
    IsGenBayesEstimator simpleBinaryHypTest (twoHypKernel Î¼ Î½)
      (binaryGenBayesEstimator Î¼ Î½ Ï€) Ï€ := by
  refine âŸ¨?_, ?_âŸ©
  Â· simp_rw [binaryGenBayesEstimator]
    refine ((measurable_discrete _).comp' (measurable_one.indicator (measurableSet_le ?_ ?_)))
      <;> fun_prop
  Â· filter_upwards [bayesInv_twoHypKernel Î¼ Î½ Ï€, twoHypKernelInv_apply' Î¼ Î½ Ï€ {true},
      twoHypKernelInv_apply' Î¼ Î½ Ï€ {false}] with x hx h_true h_false
    refine le_antisymm (le_iInf fun b â†¦ ?_) (iInf_le _ _)
    cases b <;> by_cases
      Ï€ {false} * (âˆ‚Î¼/âˆ‚twoHypKernel Î¼ Î½ âˆ˜â‚˜ Ï€) x â‰¤ Ï€ {true} * (âˆ‚Î½/âˆ‚twoHypKernel Î¼ Î½ âˆ˜â‚˜ Ï€) x
      <;> simp_all [Bool.lintegral_bool, binaryGenBayesEstimator, Bool.ofNat, -not_le, le_of_not_ge]

noncomputable instance (Î¼ Î½ : Measure ğ’³) [IsFiniteMeasure Î¼] [IsFiniteMeasure Î½]
    (Ï€ : Measure Bool) [IsFiniteMeasure Ï€] :
    HasGenBayesEstimator simpleBinaryHypTest (twoHypKernel Î¼ Î½) Ï€ :=
  âŸ¨binaryGenBayesEstimator Î¼ Î½ Ï€, binaryGenBayesEstimator_isGenBayesEstimator Î¼ Î½ Ï€âŸ©

end SimpleBinaryHypTest

/-- The Bayes risk of simple binary hypothesis testing with respect to a prior. -/
noncomputable
def bayesBinaryRisk (Î¼ Î½ : Measure ğ’³) (Ï€ : Measure Bool) : â„â‰¥0âˆ :=
  bayesRiskPrior simpleBinaryHypTest (twoHypKernel Î¼ Î½) Ï€

lemma bayesBinaryRisk_eq (Î¼ Î½ : Measure ğ’³) (Ï€ : Measure Bool) :
    bayesBinaryRisk Î¼ Î½ Ï€
      = â¨… (Îº : Kernel ğ’³ Bool) (_ : IsMarkovKernel Îº),
        Ï€ {true} * (Îº âˆ˜â‚˜ Î½) {false} + Ï€ {false} * (Îº âˆ˜â‚˜ Î¼) {true} := by
  rw [bayesBinaryRisk, bayesRiskPrior]
  congr with Îº
  congr with _
  rw [bayesianRisk, lintegral_fintype, mul_comm (Ï€ {false}), mul_comm (Ï€ {true})]
  simp

variable {Ï€ : Measure Bool}

/-- `B (aâ€¢Î¼, bâ€¢Î½; Ï€) = B (Î¼, Î½; (a*Ï€â‚€, b*Ï€â‚)).` -/
lemma bayesBinaryRisk_smul_smul (Î¼ Î½ : Measure ğ’³) (Ï€ : Measure Bool) (a b : â„â‰¥0âˆ) :
    bayesBinaryRisk (a â€¢ Î¼) (b â€¢ Î½) Ï€
      = bayesBinaryRisk Î¼ Î½ (Ï€.withDensity (fun x â†¦ bif x then b else a)) := by
  simp [bayesBinaryRisk_eq, Measure.comp_smul_left, lintegral_dirac, mul_assoc]

lemma bayesBinaryRisk_eq_bayesBinaryRisk_one_one (Î¼ Î½ : Measure ğ’³) (Ï€ : Measure Bool) :
    bayesBinaryRisk Î¼ Î½ Ï€
      = bayesBinaryRisk (Ï€ {false} â€¢ Î¼) (Ï€ {true} â€¢ Î½) (Bool.boolMeasure 1 1) := by
  rw [bayesBinaryRisk_smul_smul, Bool.measure_eq_boolMeasure Ï€, Bool.boolMeasure_withDensity]
  simp

/-- **Data processing inequality** for the Bayes binary risk. -/
lemma bayesBinaryRisk_le_bayesBinaryRisk_comp (Î¼ Î½ : Measure ğ’³) (Ï€ : Measure Bool)
    (Î· : Kernel ğ’³ ğ’³') [IsMarkovKernel Î·] :
    bayesBinaryRisk Î¼ Î½ Ï€ â‰¤ bayesBinaryRisk (Î· âˆ˜â‚˜ Î¼) (Î· âˆ˜â‚˜ Î½) Ï€ :=
  (bayesRiskPrior_le_bayesRiskPrior_comp _ _ _ Î·).trans_eq (by simp [bayesBinaryRisk])

lemma nonempty_subtype_isMarkovKernel_of_nonempty {ğ’³ : Type*} {mğ’³ : MeasurableSpace ğ’³}
    {ğ’´ : Type*} {mğ’´ : MeasurableSpace ğ’´} [Nonempty ğ’´] :
    Nonempty (Subtype (@IsMarkovKernel ğ’³ ğ’´ mğ’³ mğ’´)) := by
  simp only [nonempty_subtype, Subtype.exists]
  let y : ğ’´ := Classical.ofNonempty
  exact âŸ¨Kernel.const _ (Measure.dirac y), inferInstanceâŸ©

@[simp]
lemma bayesBinaryRisk_self (Î¼ : Measure ğ’³) (Ï€ : Measure Bool) :
    bayesBinaryRisk Î¼ Î¼ Ï€ = min (Ï€ {false}) (Ï€ {true}) * Î¼ Set.univ := by
  rw [bayesBinaryRisk_eq]
  refine le_antisymm ?_ ?_
  Â· let Î· : Kernel ğ’³ Bool :=
      if Ï€ {true} â‰¤ Ï€ {false} then (Kernel.const ğ’³ (Measure.dirac false))
        else (Kernel.const ğ’³ (Measure.dirac true))
    refine iInf_le_of_le Î· ?_
    simp_rw [Î·]
    convert iInf_le _ ?_ using 1
    Â· split_ifs with h <;> simp [le_of_not_ge, h]
    Â· split_ifs <;> infer_instance
  Â· calc
      _ â‰¥ â¨… Îº, â¨… (_ : IsMarkovKernel Îº), min (Ï€ {false}) (Ï€ {true}) * (Îº âˆ˜â‚˜ Î¼) {false}
          + min (Ï€ {false}) (Ï€ {true}) * (Îº âˆ˜â‚˜ Î¼) {true} := by
        gcongr <;> simp
      _ = â¨… Îº, â¨… (_ : IsMarkovKernel Îº), min (Ï€ {false}) (Ï€ {true}) * Î¼ Set.univ := by
        simp_rw [â† mul_add, â† measure_union (show Disjoint {false} {true} from by simp)
          (by trivial), (set_fintype_card_eq_univ_iff ({false} âˆª {true})).mp rfl,
          Measure.comp_apply_univ]
        rfl
      _ = _ := by
        rw [iInf_subtype']
        convert iInf_const
        exact nonempty_subtype_isMarkovKernel_of_nonempty

lemma bayesBinaryRisk_dirac (a b : â„â‰¥0âˆ) (x : ğ’³) (Ï€ : Measure Bool) :
    bayesBinaryRisk (a â€¢ Measure.dirac x) (b â€¢ Measure.dirac x) Ï€
      = min (Ï€ {false} * a) (Ï€ {true} * b) := by
  rw [bayesBinaryRisk_smul_smul, bayesBinaryRisk_self]
  simp [lintegral_dirac]

lemma bayesBinaryRisk_le_min (Î¼ Î½ : Measure ğ’³) (Ï€ : Measure Bool) :
    bayesBinaryRisk Î¼ Î½ Ï€ â‰¤ min (Ï€ {false} * Î¼ Set.univ) (Ï€ {true} * Î½ Set.univ) := by
  convert bayesBinaryRisk_le_bayesBinaryRisk_comp Î¼ Î½ Ï€ (Kernel.discard ğ’³)
  simp_rw [Measure.comp_discard, bayesBinaryRisk_dirac]

@[simp] lemma bayesBinaryRisk_zero_left : bayesBinaryRisk 0 Î½ Ï€ = 0 :=
  le_antisymm ((bayesBinaryRisk_le_min _ _ _).trans (by simp)) zero_le'

@[simp] lemma bayesBinaryRisk_zero_right : bayesBinaryRisk Î¼ 0 Ï€ = 0 :=
  le_antisymm ((bayesBinaryRisk_le_min _ _ _).trans (by simp)) zero_le'

@[simp] lemma bayesBinaryRisk_zero_prior : bayesBinaryRisk Î¼ Î½ 0 = 0 :=
  le_antisymm ((bayesBinaryRisk_le_min _ _ _).trans (by simp)) zero_le'

lemma bayesBinaryRisk_ne_top (Î¼ Î½ : Measure ğ’³) [IsFiniteMeasure Î¼]
    (Ï€ : Measure Bool) [IsFiniteMeasure Ï€] :
    bayesBinaryRisk Î¼ Î½ Ï€ â‰  âˆ := by
  refine lt_top_iff_ne_top.mp ((bayesBinaryRisk_le_min Î¼ Î½ Ï€).trans_lt ?_)
  exact min_lt_iff.mpr <| Or.inl <| ENNReal.mul_lt_top (measure_lt_top Ï€ _) (measure_lt_top Î¼ _)

lemma bayesBinaryRisk_of_measure_true_eq_zero (Î¼ Î½ : Measure ğ’³) (hÏ€ : Ï€ {true} = 0) :
    bayesBinaryRisk Î¼ Î½ Ï€ = 0 := by
  refine le_antisymm ?_ (zero_le _)
  convert bayesBinaryRisk_le_min Î¼ Î½ Ï€
  simp [hÏ€]

lemma bayesBinaryRisk_of_measure_false_eq_zero (Î¼ Î½ : Measure ğ’³) (hÏ€ : Ï€ {false} = 0) :
    bayesBinaryRisk Î¼ Î½ Ï€ = 0 := by
  refine le_antisymm ?_ (zero_le _)
  convert bayesBinaryRisk_le_min Î¼ Î½ Ï€
  simp [hÏ€]

lemma bayesBinaryRisk_symm (Î¼ Î½ : Measure ğ’³) (Ï€ : Measure Bool) :
    bayesBinaryRisk Î¼ Î½ Ï€ = bayesBinaryRisk Î½ Î¼ (Ï€.map Bool.not) := by
  have : (Bool.not â»Â¹' {true}) = {false} := by ext x; simp
  have h1 : (Measure.map Bool.not Ï€) {true} = Ï€ {false} := by
    rw [Measure.map_apply (by exact fun _ a â†¦ a) (by trivial), this]
  have : (Bool.not â»Â¹' {false}) = {true} := by ext x; simp
  have h2 : (Measure.map Bool.not Ï€) {false} = Ï€ {true} := by
    rw [Measure.map_apply (by exact fun _ a â†¦ a) (by trivial), this]
  simp_rw [bayesBinaryRisk_eq, h1, h2, add_comm, iInf_subtype']
  -- from this point on the proof is basically a change of variable inside the iInf,
  -- to do this I define an equivalence between `Subtype IsMarkovKernel` and itself through
  -- the `Bool.not` operation, maybe it can be shortened or something can be separated as
  -- a different lemma, but I'm not sure how useful this would be
  let e : (Kernel ğ’³ Bool) â‰ƒ (Kernel ğ’³ Bool) := by
    have h_id : Kernel.comap (Kernel.deterministic Bool.not (fun _ a â†¦ a)) Bool.not (fun _ a â†¦ a)
        = Kernel.id := by
      ext x : 1
      simp_rw [Kernel.comap_apply, Kernel.deterministic_apply, Kernel.id_apply, Bool.not_not]
    refine âŸ¨fun Îº â†¦ (Kernel.deterministic Bool.not (fun _ a â†¦ a)) âˆ˜â‚– Îº,
      fun Îº â†¦ (Kernel.deterministic Bool.not (fun _ a â†¦ a)) âˆ˜â‚– Îº, fun Îº â†¦ ?_, fun Îº â†¦ ?_âŸ© <;>
    Â· dsimp
      ext x : 1
      rw [â† Kernel.comp_assoc, Kernel.comp_deterministic_eq_comap, h_id, Kernel.id_comp]
  let e' : (Subtype (@IsMarkovKernel ğ’³ Bool _ _)) â‰ƒ (Subtype (@IsMarkovKernel ğ’³ Bool _ _)) := by
    refine âŸ¨fun âŸ¨Îº, _âŸ© â†¦ âŸ¨e Îº, ?_âŸ©, fun âŸ¨Îº, _âŸ© â†¦ âŸ¨e.symm Îº, ?_âŸ©, fun Îº â†¦ by simp, fun Îº â†¦ by simpâŸ©
      <;> simp only [Equiv.coe_fn_mk, Equiv.coe_fn_symm_mk, e] <;> infer_instance
  rw [â† Equiv.iInf_comp e']
  congr with Îº
  simp only [Equiv.coe_fn_mk, Equiv.coe_fn_symm_mk, MeasurableSpace.measurableSet_top, e', e]
  have h3 b : Set.indicator {true} (1 : Bool â†’ â„â‰¥0âˆ) b.not = Set.indicator {false} 1 b := by
    cases b <;> simp
  have h4 b : Set.indicator {false} (1 : Bool â†’ â„â‰¥0âˆ) b.not = Set.indicator {true} 1 b := by
    cases b <;> simp
  congr 2 <;>
  Â· rw [Measure.bind_apply (by trivial) (Kernel.measurable _),
      Measure.bind_apply (by trivial) (Kernel.measurable _)]
    congr with x
    rw [Kernel.comp_apply']
    simp only [Measure.dirac_apply' _ (show MeasurableSet {true} by trivial),
      Measure.dirac_apply' _ (show MeasurableSet {false} by trivial), Kernel.deterministic_apply]
    swap; trivial
    simp [h3, h4]

lemma bayesianRisk_binary_of_deterministic_indicator (Î¼ Î½ : Measure ğ’³) (Ï€ : Measure Bool)
    {E : Set ğ’³} (hE : MeasurableSet E) :
    bayesianRisk simpleBinaryHypTest (twoHypKernel Î¼ Î½)
      (Kernel.deterministic (fun x â†¦ Bool.ofNat (E.indicator 1 x))
        ((measurable_discrete _).comp' (measurable_one.indicator hE))) Ï€
      = Ï€ {false} * Î¼ E + Ï€ {true} * Î½ Eá¶œ := by
  have h_meas : Measurable fun x â†¦ Bool.ofNat (E.indicator 1 x) :=
    (measurable_discrete _).comp' (measurable_one.indicator hE)
  have h1 : (fun x â†¦ Bool.ofNat (E.indicator 1 x)) â»Â¹' {false} = Eá¶œ := by
    ext; simp [Bool.ofNat]
  have h2 : (fun x â†¦ Bool.ofNat (E.indicator 1 x)) â»Â¹' {true} = E := by
    ext; simp [Bool.ofNat]
  rw [bayesianRisk, Bool.lintegral_bool, mul_comm (Ï€ {false}), mul_comm (Ï€ {true})]
  simp only [risk_simpleBinaryHypTest_false, MeasurableSpace.measurableSet_top,
    risk_simpleBinaryHypTest_true]
  simp_rw [Measure.comp_deterministic_eq_map, Measure.map_apply h_meas trivial, h1, h2]

lemma bayesBinaryRisk_eq_iInf_measurableSet (Î¼ Î½ : Measure ğ’³) [IsFiniteMeasure Î¼]
    [IsFiniteMeasure Î½] (Ï€ : Measure Bool) [IsFiniteMeasure Ï€] :
    bayesBinaryRisk Î¼ Î½ Ï€ = â¨… E, â¨… (_ : MeasurableSet E), Ï€ {false} * Î¼ E + Ï€ {true} * Î½ Eá¶œ := by
  apply le_antisymm
  Â· simp_rw [le_iInf_iff, bayesBinaryRisk, bayesRiskPrior]
    intro E hE
    rw [â† bayesianRisk_binary_of_deterministic_indicator _ _ _ hE]
    exact iInf_le_of_le _ (iInf_le _ (Kernel.isMarkovKernel_deterministic _))
  Â· let E := {x | Ï€ {false} * (âˆ‚Î¼/âˆ‚twoHypKernel Î¼ Î½ âˆ˜â‚˜ Ï€) x
      â‰¤ Ï€ {true} * (âˆ‚Î½/âˆ‚twoHypKernel Î¼ Î½ âˆ˜â‚˜ Ï€) x}
    have hE : MeasurableSet E := measurableSet_le (by fun_prop) (by fun_prop)
    rw [bayesBinaryRisk, â† isBayesEstimator_of_isGenBayesEstimator
      (binaryGenBayesEstimator_isGenBayesEstimator Î¼ Î½ Ï€), IsGenBayesEstimator.Kernel]
    simp_rw [binaryGenBayesEstimator, bayesianRisk_binary_of_deterministic_indicator _ _ _ hE]
    exact iInf_le_of_le E (iInf_le _ hE)

lemma bayesBinaryRisk_eq_lintegral_min (Î¼ Î½ : Measure ğ’³) [IsFiniteMeasure Î¼]
    [IsFiniteMeasure Î½] (Ï€ : Measure Bool) [IsFiniteMeasure Ï€] :
    bayesBinaryRisk Î¼ Î½ Ï€ = âˆ«â» x, min (Ï€ {false} * Î¼.rnDeriv (twoHypKernel Î¼ Î½ âˆ˜â‚˜ Ï€) x)
      (Ï€ {true} * Î½.rnDeriv (twoHypKernel Î¼ Î½ âˆ˜â‚˜ Ï€) x) âˆ‚(twoHypKernel Î¼ Î½ âˆ˜â‚˜ Ï€) := by
  simp_rw [bayesBinaryRisk, bayesRiskPrior_eq_of_hasGenBayesEstimator_binary, Bool.iInf_bool]
  simp [min_comm]

lemma toReal_bayesBinaryRisk_eq_integral_min (Î¼ Î½ : Measure ğ’³) [IsFiniteMeasure Î¼]
    [IsFiniteMeasure Î½] (Ï€ : Measure Bool) [IsFiniteMeasure Ï€] :
    (bayesBinaryRisk Î¼ Î½ Ï€).toReal
      = âˆ« x, min (Ï€ {false} * Î¼.rnDeriv (twoHypKernel Î¼ Î½ âˆ˜â‚˜ Ï€) x).toReal
        (Ï€ {true} * Î½.rnDeriv (twoHypKernel Î¼ Î½ âˆ˜â‚˜ Ï€) x).toReal âˆ‚(twoHypKernel Î¼ Î½ âˆ˜â‚˜ Ï€) := by
  rw [bayesBinaryRisk_eq_lintegral_min, integral_eq_lintegral_of_nonneg_ae]
  rotate_left
  Â· filter_upwards with x; positivity
  Â· refine Measurable.aestronglyMeasurable <| Measurable.min ?_ ?_
      <;> exact Measure.measurable_rnDeriv _ _ |>.const_mul _ |>.ennreal_toNNReal |>.coe_nnreal_real
  congr 1
  apply lintegral_congr_ae
  filter_upwards [Measure.rnDeriv_ne_top Î¼ _, Measure.rnDeriv_ne_top Î½ _] with x hxÎ¼ hxÎ½
  have : (Ï€ {false} * Î¼.rnDeriv (twoHypKernel Î¼ Î½ âˆ˜â‚˜ Ï€) x) â‰  âŠ¤ :=
    (ENNReal.mul_ne_top (measure_ne_top _ _) hxÎ¼)
  have : (Ï€ {true} * Î½.rnDeriv (twoHypKernel Î¼ Î½ âˆ˜â‚˜ Ï€) x) â‰  âŠ¤ :=
    (ENNReal.mul_ne_top (measure_ne_top _ _) hxÎ½)
  rcases le_total (Ï€ {false} * Î¼.rnDeriv (twoHypKernel Î¼ Î½ âˆ˜â‚˜ Ï€) x)
    (Ï€ {true} * Î½.rnDeriv (twoHypKernel Î¼ Î½ âˆ˜â‚˜ Ï€) x) with h | h
  all_goals
  Â· have h' := (ENNReal.toReal_le_toReal (by assumption) (by assumption)).mpr h
    simp only [h, h', min_eq_left, min_eq_right]
    exact (ENNReal.ofReal_toReal_eq_iff.mpr (by assumption)).symm

lemma toReal_bayesBinaryRisk_eq_integral_abs (Î¼ Î½ : Measure ğ’³) [IsFiniteMeasure Î¼]
    [IsFiniteMeasure Î½] (Ï€ : Measure Bool) [IsFiniteMeasure Ï€] :
    (bayesBinaryRisk Î¼ Î½ Ï€).toReal
      = 2â»Â¹ * (((twoHypKernel Î¼ Î½ âˆ˜â‚˜ Ï€) Set.univ).toReal
        - âˆ« x, |(Ï€ {false} * Î¼.rnDeriv (twoHypKernel Î¼ Î½ âˆ˜â‚˜ Ï€) x).toReal
          - (Ï€ {true} * Î½.rnDeriv (twoHypKernel Î¼ Î½ âˆ˜â‚˜ Ï€) x).toReal| âˆ‚(twoHypKernel Î¼ Î½ âˆ˜â‚˜ Ï€)) := by
  simp_rw [toReal_bayesBinaryRisk_eq_integral_min, min_eq_add_sub_abs_sub, integral_mul_left]
  congr
  have hÎ¼_int : Integrable (fun x â†¦ (Ï€ {false} * Î¼.rnDeriv (twoHypKernel Î¼ Î½ âˆ˜â‚˜ Ï€) x).toReal)
      (twoHypKernel Î¼ Î½ âˆ˜â‚˜ Ï€) := by
    simp_rw [ENNReal.toReal_mul]
    exact Integrable.const_mul Measure.integrable_toReal_rnDeriv _
  have hÎ½_int : Integrable (fun x â†¦ (Ï€ {true} * Î½.rnDeriv (twoHypKernel Î¼ Î½ âˆ˜â‚˜ Ï€) x).toReal)
      (twoHypKernel Î¼ Î½ âˆ˜â‚˜ Ï€) := by
    simp_rw [ENNReal.toReal_mul]
    exact Integrable.const_mul Measure.integrable_toReal_rnDeriv _
  have h_int_abs : Integrable (fun x â†¦ |(Ï€ {false} * Î¼.rnDeriv (twoHypKernel Î¼ Î½ âˆ˜â‚˜ Ï€) x).toReal
      - (Ï€ {true} * Î½.rnDeriv (twoHypKernel Î¼ Î½ âˆ˜â‚˜ Ï€) x).toReal|) (twoHypKernel Î¼ Î½ âˆ˜â‚˜ Ï€) :=
    hÎ¼_int.sub hÎ½_int |>.abs
  rw [integral_sub (by exact hÎ¼_int.add hÎ½_int) h_int_abs, integral_add hÎ¼_int hÎ½_int]
  simp only [ENNReal.toReal_mul, MeasurableSet.univ, sub_left_inj, integral_mul_left]
  nth_rw 5 [measure_comp_twoHypKernel]
  calc
    _ = (Ï€ {false}).toReal * (Î¼ Set.univ).toReal + (Ï€ {true}).toReal
        * âˆ« (a : ğ’³), ((âˆ‚Î½/âˆ‚twoHypKernel Î¼ Î½ âˆ˜â‚˜ Ï€) a).toReal âˆ‚(twoHypKernel Î¼ Î½ âˆ˜â‚˜ Ï€) := by
      by_cases hÏ€_false : Ï€ {false} = 0
      Â· simp [hÏ€_false, bayesBinaryRisk_of_measure_false_eq_zero]
      rw [Measure.integral_toReal_rnDeriv
        (absolutelyContinuous_measure_comp_twoHypKernel_left Î¼ Î½ hÏ€_false)]
    _ = (Ï€ {false}).toReal * (Î¼ Set.univ).toReal + (Ï€ {true}).toReal * (Î½ Set.univ).toReal := by
      by_cases hÏ€_true : Ï€ {true} = 0
      Â· simp [hÏ€_true, bayesBinaryRisk_of_measure_true_eq_zero]
      rw [Measure.integral_toReal_rnDeriv
        (absolutelyContinuous_measure_comp_twoHypKernel_right Î¼ Î½ hÏ€_true)]
    _ = _ := by
      simp_rw [add_comm, Measure.coe_add, Measure.coe_smul, Pi.add_apply, Pi.smul_apply,
        smul_eq_mul, ENNReal.toReal_add (ENNReal.mul_ne_top (measure_ne_top _ _)
        (measure_ne_top _ _)) (ENNReal.mul_ne_top (measure_ne_top _ _) (measure_ne_top _ _)),
        ENNReal.toReal_mul]

lemma bayesBinaryRisk_eq_lintegral_ennnorm (Î¼ Î½ : Measure ğ’³) [IsFiniteMeasure Î¼]
    [IsFiniteMeasure Î½] (Ï€ : Measure Bool) [IsFiniteMeasure Ï€] :
    bayesBinaryRisk Î¼ Î½ Ï€ = 2â»Â¹ * (((twoHypKernel Î¼ Î½ âˆ˜â‚˜ Ï€) Set.univ)
        - âˆ«â» x, â€–(Ï€ {false} * (âˆ‚Î¼/âˆ‚(twoHypKernel Î¼ Î½ âˆ˜â‚˜ Ï€)) x).toReal
          - (Ï€ {true} * (âˆ‚Î½/âˆ‚(twoHypKernel Î¼ Î½ âˆ˜â‚˜ Ï€)) x).toRealâ€–â‚Š âˆ‚(twoHypKernel Î¼ Î½ âˆ˜â‚˜ Ï€)) := by
  rw [â† ENNReal.ofReal_toReal (bayesBinaryRisk_ne_top Î¼ Î½ Ï€),
    toReal_bayesBinaryRisk_eq_integral_abs, ENNReal.ofReal_mul (inv_nonneg.mpr zero_le_two),
    ENNReal.ofReal_inv_of_pos zero_lt_two, ENNReal.ofReal_ofNat,
    ENNReal.ofReal_sub _ (by positivity), ENNReal.ofReal_toReal (measure_ne_top _ _),
    MeasureTheory.ofReal_integral_eq_lintegral_ofReal _
    (.of_forall fun _ â†¦ by positivity)]
  swap
  Â· refine âŸ¨Measurable.aestronglyMeasurable (by fun_prop), ?_âŸ©
    simp_rw [HasFiniteIntegral, Real.nnnorm_abs]
    calc
      _ â‰¤ âˆ«â» a, â€–(Ï€ {false} * (âˆ‚Î¼/âˆ‚twoHypKernel Î¼ Î½ âˆ˜â‚˜ Ï€) a).toRealâ€–â‚Š +
          â€–(Ï€ {true} * (âˆ‚Î½/âˆ‚twoHypKernel Î¼ Î½ âˆ˜â‚˜ Ï€) a).toRealâ€–â‚Š âˆ‚(twoHypKernel Î¼ Î½ âˆ˜â‚˜ Ï€) := by
        gcongr
        exact_mod_cast nnnorm_sub_le _ _
      _ = âˆ«â» a, â€–(Ï€ {false} * (âˆ‚Î¼/âˆ‚twoHypKernel Î¼ Î½ âˆ˜â‚˜ Ï€) a).toRealâ€–â‚Š âˆ‚(twoHypKernel Î¼ Î½ âˆ˜â‚˜ Ï€) +
          âˆ«â» a, â€–(Ï€ {true} * (âˆ‚Î½/âˆ‚twoHypKernel Î¼ Î½ âˆ˜â‚˜ Ï€) a).toRealâ€–â‚Š âˆ‚(twoHypKernel Î¼ Î½ âˆ˜â‚˜ Ï€) :=
        lintegral_add_left (by fun_prop) _
      _ â‰¤ Ï€ {false} * âˆ«â» a, â€–((âˆ‚Î¼/âˆ‚twoHypKernel Î¼ Î½ âˆ˜â‚˜ Ï€) a).toRealâ€–â‚Š âˆ‚(twoHypKernel Î¼ Î½ âˆ˜â‚˜ Ï€) +
          Ï€ {true} * âˆ«â» a, â€–((âˆ‚Î½/âˆ‚twoHypKernel Î¼ Î½ âˆ˜â‚˜ Ï€) a).toRealâ€–â‚Š âˆ‚(twoHypKernel Î¼ Î½ âˆ˜â‚˜ Ï€) := by
        simp_rw [ENNReal.toReal_mul, nnnorm_mul, ENNReal.coe_mul]
        rw [lintegral_const_mul _ (by fun_prop), lintegral_const_mul _ (by fun_prop)]
        gcongr <;>
        Â· rw [Real.ennnorm_eq_ofReal_abs, ENNReal.abs_toReal]
          exact ENNReal.ofReal_toReal_le
      _ â‰¤ Ï€ {false} * âˆ«â» a, (âˆ‚Î¼/âˆ‚twoHypKernel Î¼ Î½ âˆ˜â‚˜ Ï€) a âˆ‚(twoHypKernel Î¼ Î½ âˆ˜â‚˜ Ï€) +
          Ï€ {true} * âˆ«â» a, (âˆ‚Î½/âˆ‚twoHypKernel Î¼ Î½ âˆ˜â‚˜ Ï€) a âˆ‚(twoHypKernel Î¼ Î½ âˆ˜â‚˜ Ï€) := by
        gcongr <;>
        Â· rw [Real.ennnorm_eq_ofReal_abs, ENNReal.abs_toReal]
          exact ENNReal.ofReal_toReal_le
      _ = Ï€ {false} * Î¼ Set.univ + Ï€ {true} * Î½ Set.univ := by
        congr 1
        Â· by_cases h_false : Ï€ {false} = 0
          Â· rw [h_false, zero_mul, zero_mul]
          rw [Measure.lintegral_rnDeriv
            (absolutelyContinuous_measure_comp_twoHypKernel_left Î¼ Î½ h_false)]
        Â· by_cases h_true : Ï€ {true} = 0
          Â· rw [h_true, zero_mul, zero_mul]
          rw [Measure.lintegral_rnDeriv
            (absolutelyContinuous_measure_comp_twoHypKernel_right Î¼ Î½ h_true)]
      _ < âŠ¤ :=
        ENNReal.add_lt_top.mpr âŸ¨ENNReal.mul_lt_top (measure_lt_top _ _) (measure_lt_top _ _),
          ENNReal.mul_lt_top (measure_lt_top _ _) (measure_lt_top _ _)âŸ©
  simp_rw [Real.ennnorm_eq_ofReal_abs]

end ProbabilityTheory
