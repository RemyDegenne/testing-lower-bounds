/-
Copyright (c) 2024 RÃ©my Degenne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: RÃ©my Degenne
-/
import TestingLowerBounds.Kernel.BayesInv
import TestingLowerBounds.ForMathlib.KernelConstComp

/-!
# Estimation and risk

## Main definitions

* `estimationProblem`
* ...

## Main statements

* `bayesRiskPrior_le_bayesRiskPrior_comp`

## Notation

## Implementation details

-/

open MeasureTheory

open scoped ENNReal NNReal

namespace ProbabilityTheory

variable {Î˜ Î˜' ğ’³ ğ’³' ğ’³'' ğ’´ ğ’µ : Type*} {mÎ˜ : MeasurableSpace Î˜} {mÎ˜' : MeasurableSpace Î˜'}
  {mğ’³ : MeasurableSpace ğ’³} {mğ’³' : MeasurableSpace ğ’³'} {mğ’³'' : MeasurableSpace ğ’³''}
  {mğ’´ : MeasurableSpace ğ’´} {mğ’µ : MeasurableSpace ğ’µ}
  {Î¼ Î½ : Measure ğ’³} {P : kernel Î˜ ğ’³} {Îº : kernel ğ’³ ğ’µ} {Ï€ : Measure Î˜}

section EstimationProblem

/-- An estimation problem: a kernel `P` from a parameter space `Î˜` to a sample space `ğ’³`,
an objective function `y` on the parameter space and a cost function `â„“`.
We don't put the data generating kernel into this structure, since we will often perform operations
on it and we don't want to duplicate all kernel operations on `estimationProblem`. -/
@[ext]
structure estimationProblem (Î˜ ğ’´ ğ’µ : Type*) [mÎ˜ : MeasurableSpace Î˜]
    [mğ’´ : MeasurableSpace ğ’´] [mğ’µ : MeasurableSpace ğ’µ] :=
  /-- The objective function. -/
  y : Î˜ â†’ ğ’´
  y_meas : Measurable y
  /-- The cost (or loss) function. -/
  â„“ : ğ’´ Ã— ğ’µ â†’ â„â‰¥0âˆ
  â„“_meas : Measurable â„“

/-- Given an estimation problem `E` and a measurable function `f : Î˜' â†’ Î˜`, we can consider a new
estimation problem where the kernel is given by the pullback of `E.P` through `f`. -/
@[simps]
noncomputable
def estimationProblem.comap (E : estimationProblem Î˜ ğ’´ ğ’µ) (f : Î˜' â†’ Î˜) (hf : Measurable f) :
    estimationProblem Î˜' ğ’´ ğ’µ where
  y := E.y âˆ˜ f
  y_meas := E.y_meas.comp hf
  â„“ := E.â„“
  â„“_meas := E.â„“_meas

end EstimationProblem

variable {E : estimationProblem Î˜ ğ’´ ğ’µ}

/-- The risk of an estimator `Îº` on an estimation problem `E` with data generating kernel `P`
at the parameter `Î¸`. -/
noncomputable
def risk (E : estimationProblem Î˜ ğ’´ ğ’µ) (P : kernel Î˜ ğ’³) (Îº : kernel ğ’³ ğ’µ) (Î¸ : Î˜) : â„â‰¥0âˆ :=
  âˆ«â» z, E.â„“ (E.y Î¸, z) âˆ‚((Îº âˆ˜â‚– P) Î¸)

/-- The bayesian risk of an estimator `Îº` on an estimation problem `E` with data generating
kernel `P` with respect to a prior `Ï€`. -/
noncomputable
def bayesianRisk (E : estimationProblem Î˜ ğ’´ ğ’µ) (P : kernel Î˜ ğ’³)
    (Îº : kernel ğ’³ ğ’µ) (Ï€ : Measure Î˜) : â„â‰¥0âˆ :=
  âˆ«â» Î¸, risk E P Îº Î¸ âˆ‚Ï€

@[simp]
lemma bayesianRisk_of_isEmpty [IsEmpty Î˜] : bayesianRisk E P Îº Ï€ = 0 := by simp [bayesianRisk]

lemma bayesianRisk_le_iSup_risk (E : estimationProblem Î˜ ğ’´ ğ’µ) (P : kernel Î˜ ğ’³) (Îº : kernel ğ’³ ğ’µ)
    (Ï€ : Measure Î˜) [IsProbabilityMeasure Ï€] :
    bayesianRisk E P Îº Ï€ â‰¤ â¨† Î¸, risk E P Îº Î¸ := by
  rw [bayesianRisk]
  calc âˆ«â» Î¸, risk E P Îº Î¸ âˆ‚Ï€
  _ â‰¤ âˆ«â» _, (â¨† Î¸', risk E P Îº Î¸') âˆ‚Ï€ := lintegral_mono (fun Î¸ â†¦ le_iSup _ _)
  _ = â¨† Î¸, risk E P Îº Î¸ := by simp

lemma bayesianRisk_comap_measurableEquiv (E : estimationProblem Î˜ ğ’´ ğ’µ) (P : kernel Î˜ ğ’³)
    [IsSFiniteKernel P]
    (Îº : kernel ğ’³ ğ’µ) [IsSFiniteKernel Îº] (Ï€ : Measure Î˜) (e : Î˜ â‰ƒáµ Î˜') :
    bayesianRisk (E.comap e.symm e.symm.measurable) (kernel.comap P e.symm e.symm.measurable)
      Îº (Ï€.map e) = bayesianRisk E P Îº Ï€ := by
  simp only [bayesianRisk, risk, estimationProblem.comap_y, Function.comp_apply,
    estimationProblem.comap_â„“]
  rw [lintegral_map _ e.measurable]
  Â· congr with Î¸
    congr -- todo: `congr with z hz` gives a warning. bug.
    ext z hz
    Â· rw [kernel.comp_apply' _ _ _ hz, kernel.comp_apply' _ _ _ hz, kernel.comap_apply]
      simp
    Â· simp
  Â· refine Measurable.lintegral_kernel_prod_right ?_
    refine E.â„“_meas.comp ?_
    exact (E.y_meas.comp (e.symm.measurable.comp measurable_fst)).prod_mk measurable_snd

/-- The Bayes risk of an estimation problem `E` with respect to a prior `Ï€`, defined as the infimum
of the Bayesian risks of all estimators. -/
noncomputable
def bayesRiskPrior (E : estimationProblem Î˜ ğ’´ ğ’µ) (P : kernel Î˜ ğ’³) (Ï€ : Measure Î˜) : â„â‰¥0âˆ :=
  â¨… (Îº : kernel ğ’³ ğ’µ) (_ : IsMarkovKernel Îº), bayesianRisk E P Îº Ï€

/-- **Data processing inequality** for the Bayes risk with respect to a prior. -/
lemma bayesRiskPrior_le_bayesRiskPrior_comp (E : estimationProblem Î˜ ğ’´ ğ’µ) (P : kernel Î˜ ğ’³)
    (Ï€ : Measure Î˜) (Î· : kernel ğ’³ ğ’³') [IsMarkovKernel Î·] :
    bayesRiskPrior E P Ï€ â‰¤ bayesRiskPrior E (Î· âˆ˜â‚– P) Ï€ := by
  simp only [bayesRiskPrior, bayesianRisk, risk, le_iInf_iff]
  intro Îº hÎº
  rw [â† kernel.comp_assoc Îº Î·]
  exact iInf_le_of_le (Îº âˆ˜â‚– Î·) (iInf_le_of_le inferInstance le_rfl)

/-- An estimator is a Bayes estimator for a prior `Ï€` if it attains the Bayes risk for `Ï€`. -/
def IsBayesEstimator (E : estimationProblem Î˜ ğ’´ ğ’µ) (P : kernel Î˜ ğ’³)
    (Îº : kernel ğ’³ ğ’µ) (Ï€ : Measure Î˜) : Prop :=
  bayesianRisk E P Îº Ï€ = bayesRiskPrior E P Ï€

/-- The Bayes risk of an estimation problem `E`, defined as the supremum over priors of the Bayes
risk of `E` with respect to the prior. -/
noncomputable
def bayesRisk (E : estimationProblem Î˜ ğ’´ ğ’µ) (P : kernel Î˜ ğ’³) : â„â‰¥0âˆ :=
  â¨† (Ï€ : Measure Î˜) (_ : IsProbabilityMeasure Ï€), bayesRiskPrior E P Ï€

/-- The Bayes risk of an estimation problem `E`, defined as the infimum over estimators of the
maximal risk of the estimator. -/
noncomputable
def minimaxRisk (E : estimationProblem Î˜ ğ’´ ğ’µ) (P : kernel Î˜ ğ’³) : â„â‰¥0âˆ :=
  â¨… (Îº : kernel ğ’³ ğ’µ) (_ : IsMarkovKernel Îº), â¨† Î¸, risk E P Îº Î¸

lemma bayesRiskPrior_le_minimaxRisk (E : estimationProblem Î˜ ğ’´ ğ’µ) (P : kernel Î˜ ğ’³)
    (Ï€ : Measure Î˜) [IsProbabilityMeasure Ï€] :
    bayesRiskPrior E P Ï€ â‰¤ minimaxRisk E P :=
  iInf_mono (fun _ â†¦ iInf_mono fun _ â†¦ bayesianRisk_le_iSup_risk _ _ _ _)

lemma bayesRisk_le_minimaxRisk (E : estimationProblem Î˜ ğ’´ ğ’µ) (P : kernel Î˜ ğ’³) :
    bayesRisk E P â‰¤ minimaxRisk E P := by
  simp only [bayesRisk, iSup_le_iff]
  exact fun _ _ â†¦ bayesRiskPrior_le_minimaxRisk _ _ _

/-! ### Properties of the Bayes risk of a prior -/

lemma bayesRiskPrior_compProd_le_bayesRiskPrior (E : estimationProblem Î˜ ğ’´ ğ’µ) (P : kernel Î˜ ğ’³)
    [IsSFiniteKernel P] (Ï€ : Measure Î˜) (Îº : kernel (Î˜ Ã— ğ’³) ğ’³') [IsMarkovKernel Îº] :
    bayesRiskPrior E (P âŠ—â‚– Îº) Ï€ â‰¤ bayesRiskPrior E P Ï€ := by
  have : P = (kernel.deterministic Prod.fst (by fun_prop)) âˆ˜â‚– (P âŠ—â‚– Îº) := by
    rw [kernel.deterministic_comp_eq_map, â† kernel.fst, kernel.fst_compProd]
  nth_rw 2 [this]
  exact bayesRiskPrior_le_bayesRiskPrior_comp _ _ _ _

-- Do we also need a version without the `IsMarkovKernel` assumption? it would be of the form:
-- `bayesRiskPrior E Ï€ â‰¤ â¨… z : ğ’µ, âˆ«â» Î¸, E.â„“ (E.y Î¸, z) * (E.P Î¸) Set.univ âˆ‚Ï€`
lemma bayesRiskPrior_le_inf (E : estimationProblem Î˜ ğ’´ ğ’µ) (P : kernel Î˜ ğ’³)
    (Ï€ : Measure Î˜) [IsMarkovKernel P] :
    bayesRiskPrior E P Ï€ â‰¤ â¨… z : ğ’µ, âˆ«â» Î¸, E.â„“ (E.y Î¸, z) âˆ‚Ï€ := by
  simp_rw [le_iInf_iff, bayesRiskPrior]
  refine fun z â†¦ iInf_le_of_le (kernel.const _ (Measure.dirac z)) ?_
  convert iInf_le _ ?_ using 1
  Â· simp_rw [bayesianRisk, risk, kernel.const_comp', kernel.const_apply]
    congr with Î¸
    rw [lintegral_dirac']
    have := E.â„“_meas
    fun_prop [E.â„“_meas]
  Â· exact kernel.isMarkovKernel_const

/-- The Bayesian risk of an estimator `Îº` with respect to a prior `Ï€` can be expressed as
an integral in the following way: `R_Ï€(Îº) = ((Pâ€ Ï€ Ã— Îº) âˆ˜ P âˆ˜ Ï€)[(Î¸, z) â†¦ â„“(y(Î¸), z)]`. -/
lemma bayesianRisk_eq_lintegral_bayesInv_prod [StandardBorelSpace Î˜] [Nonempty Î˜]
    (E : estimationProblem Î˜ ğ’´ ğ’µ) (P : kernel Î˜ ğ’³) [IsFiniteKernel P] (Îº : kernel ğ’³ ğ’µ)
    (Ï€ : Measure Î˜) [IsFiniteMeasure Ï€] [IsSFiniteKernel Îº] :
    bayesianRisk E P Îº Ï€ = âˆ«â» (Î¸z : Î˜ Ã— ğ’µ), E.â„“ (E.y Î¸z.1, Î¸z.2) âˆ‚(Ï€ âˆ˜â‚˜ P âˆ˜â‚˜ ((Pâ€ Ï€) Ã—â‚– Îº)) := by
  have := E.â„“_meas
  have := E.y_meas
  simp only [bayesianRisk, risk]
  rw [â† MeasureTheory.Measure.lintegral_compProd (f := fun Î¸z â†¦ E.â„“ (E.y Î¸z.1, Î¸z.2)) (by fun_prop),
    â† kernel.swap_prod, kernel.prod_eq_copy_comp_parallelComp, Measure.compProd_eq_comp,
    kernel.prod_eq_copy_comp_parallelComp]
  nth_rw 2 [â† kernel.parallelComp_comp_id_right_left]
  simp_rw [â† Measure.comp_assoc, compProd_bayesInv'', Measure.comp_assoc, â† kernel.comp_assoc,
  kernel.swap_parallelComp, kernel.comp_assoc (_ âˆ¥â‚– Îº), kernel.swap_parallelComp, kernel.comp_assoc,
  kernel.swap_copy, â† kernel.comp_assoc, kernel.parallelComp_comp_id_left_left]

lemma bayesianRisk_eq_integral_integral_integral [StandardBorelSpace Î˜] [Nonempty Î˜]
    (E : estimationProblem Î˜ ğ’´ ğ’µ) (P : kernel Î˜ ğ’³) [IsFiniteKernel P] (Îº : kernel ğ’³ ğ’µ)
    (Ï€ : Measure Î˜) [IsFiniteMeasure Ï€] [IsSFiniteKernel Îº] :
    bayesianRisk E P Îº Ï€ = âˆ«â» x, âˆ«â» z, âˆ«â» Î¸, E.â„“ (E.y Î¸, z) âˆ‚(Pâ€ Ï€) x âˆ‚Îº x âˆ‚Ï€ âˆ˜â‚˜ P := by
  have := E.â„“_meas
  have := E.y_meas
  rw [bayesianRisk_eq_lintegral_bayesInv_prod,
    Measure.lintegral_bind (kernel.measurable ((Pâ€ Ï€) Ã—â‚– Îº)) (by fun_prop)]
  congr with x
  rw [kernel.prod_apply, lintegral_prod_symm' _ (by fun_prop)]

lemma bayesianRisk_ge_lintegral_iInf_bayesInv [StandardBorelSpace Î˜] [Nonempty Î˜]
    (E : estimationProblem Î˜ ğ’´ ğ’µ) (P : kernel Î˜ ğ’³) [IsFiniteKernel P] (Îº : kernel ğ’³ ğ’µ)
    (Ï€ : Measure Î˜) [IsFiniteMeasure Ï€] [IsMarkovKernel Îº] :
    bayesianRisk E P Îº Ï€ â‰¥ âˆ«â» x, â¨… z : ğ’µ, âˆ«â» Î¸, E.â„“ (E.y Î¸, z) âˆ‚((Pâ€ Ï€) x) âˆ‚(Ï€ âˆ˜â‚˜ P) := by
  rw [bayesianRisk_eq_integral_integral_integral]
  gcongr with x
  calc
    _ â‰¥ âˆ«â» _, â¨… z, âˆ«â» (Î¸ : Î˜), E.â„“ (E.y Î¸, z) âˆ‚(Pâ€ Ï€) x âˆ‚Îº x :=
      lintegral_mono fun z â†¦ iInf_le' _ z
    _ = â¨… z, âˆ«â» (Î¸ : Î˜), E.â„“ (E.y Î¸, z) âˆ‚(Pâ€ Ï€) x := by
      rw [lintegral_const, measure_univ, mul_one]

/-! ### Generalized Bayes estimator -/

/-- We say that a measurable function `f : ğ’³ â†’ ğ’µ` is a Generalized Bayes estimator for the
estimation problem `E` with respect to the prior `Ï€` if for `(Ï€ âˆ˜â‚˜ P)`-almost every `x` it is of
the form `x â†¦ argmin_z Pâ€ Ï€(x)[Î¸ â†¦ â„“(y(Î¸), z)]`.-/
structure IsGenBayesEstimator [StandardBorelSpace Î˜] [Nonempty Î˜]
    (E : estimationProblem Î˜ ğ’´ ğ’µ) (P : kernel Î˜ ğ’³) [IsFiniteKernel P] (f : ğ’³ â†’ ğ’µ)
    (Ï€ : Measure Î˜) [IsFiniteMeasure Ï€] : Prop where
  measurable : Measurable f
  property : âˆ€áµ x âˆ‚Ï€ âˆ˜â‚˜ P, âˆ«â» Î¸, E.â„“ (E.y Î¸, f x) âˆ‚(Pâ€ Ï€) x
    = â¨… z, âˆ«â» Î¸, E.â„“ (E.y Î¸, z) âˆ‚(Pâ€ Ï€) x

/-- Given a Generalized Bayes estimator `f`, we can define a deterministic kernel. -/
noncomputable
abbrev IsGenBayesEstimator.kernel [StandardBorelSpace Î˜] [Nonempty Î˜]
    {E : estimationProblem Î˜ ğ’´ ğ’µ} {P : kernel Î˜ ğ’³} [IsFiniteKernel P]
    {Ï€ : Measure Î˜} [IsFiniteMeasure Ï€]
    {f : ğ’³ â†’ ğ’µ} (h : IsGenBayesEstimator E P f Ï€) : kernel ğ’³ ğ’µ :=
  kernel.deterministic f h.measurable

lemma bayesianRisk_of_isGenBayesEstimator [StandardBorelSpace Î˜] [Nonempty Î˜]
    {E : estimationProblem Î˜ ğ’´ ğ’µ} {P : kernel Î˜ ğ’³} [IsFiniteKernel P]
    {Ï€ : Measure Î˜} [IsFiniteMeasure Ï€]
    {f : ğ’³ â†’ ğ’µ} (hf : IsGenBayesEstimator E P f Ï€) :
    bayesianRisk E P hf.kernel Ï€
      = âˆ«â» x, â¨… z, âˆ«â» Î¸, E.â„“ (E.y Î¸, z) âˆ‚(Pâ€ Ï€) x âˆ‚Ï€ âˆ˜â‚˜ P := by
  have := E.â„“_meas
  have := E.y_meas
  rw [bayesianRisk_eq_integral_integral_integral]
  refine lintegral_congr_ae ?_
  filter_upwards [hf.property] with x hx
  rwa [kernel.deterministic_apply,
    lintegral_dirac' _ (Measurable.lintegral_prod_left (by fun_prop))]

lemma isBayesEstimator_of_isGenBayesEstimator [StandardBorelSpace Î˜] [Nonempty Î˜]
    {E : estimationProblem Î˜ ğ’´ ğ’µ} {P : kernel Î˜ ğ’³} [IsFiniteKernel P]
    {Ï€ : Measure Î˜} [IsFiniteMeasure Ï€]
    {f : ğ’³ â†’ ğ’µ} (hf : IsGenBayesEstimator E P f Ï€) :
    IsBayesEstimator E P hf.kernel Ï€ := by
  simp_rw [IsBayesEstimator, bayesRiskPrior]
  apply le_antisymm
  Â· rw [bayesianRisk_of_isGenBayesEstimator hf]
    simp_all [bayesianRisk_ge_lintegral_iInf_bayesInv]
  Â· refine iInf_le_of_le hf.kernel ?_
    exact iInf_le _ (kernel.isMarkovKernel_deterministic hf.measurable)

/-- The estimation problem `E` admits a Generalized Bayes estimator with respect to
the prior `Ï€`. -/
class HasGenBayesEstimator [StandardBorelSpace Î˜] [Nonempty Î˜] (E : estimationProblem Î˜ ğ’´ ğ’µ)
    (P : kernel Î˜ ğ’³) [IsFiniteKernel P] (Ï€ : Measure Î˜) [IsFiniteMeasure Ï€] where
  /-- The Generalized Bayes estimator. -/
  estimator : ğ’³ â†’ ğ’µ
  property : IsGenBayesEstimator E P estimator Ï€

lemma bayesRiskPrior_eq_of_hasGenBayesEstimator [StandardBorelSpace Î˜] [Nonempty Î˜]
    (E : estimationProblem Î˜ ğ’´ ğ’µ) (P : kernel Î˜ ğ’³) [IsFiniteKernel P]
    (Ï€ : Measure Î˜) [IsFiniteMeasure Ï€]
    [h : HasGenBayesEstimator E P Ï€] :
    bayesRiskPrior E P Ï€ = âˆ«â» x, â¨… z, âˆ«â» Î¸, E.â„“ (E.y Î¸, z) âˆ‚((Pâ€ Ï€) x) âˆ‚(Ï€ âˆ˜â‚˜ P) := by
  rw [â† isBayesEstimator_of_isGenBayesEstimator h.property, bayesianRisk_of_isGenBayesEstimator]

/-! ### Bayes risk increase -/

/-- The Bayes risk increase of an estimation problem `E` with respect to a prior `Ï€` and a kernel
`Î·` gives us how much the composition with `Î·` increases the risk of `E` with respect to `Ï€`. -/
noncomputable
def bayesRiskIncrease (E : estimationProblem Î˜ ğ’´ ğ’µ) (P : kernel Î˜ ğ’³) (Ï€ : Measure Î˜)
    (Î· : kernel ğ’³ ğ’³') : â„â‰¥0âˆ :=
  bayesRiskPrior E (Î· âˆ˜â‚– P) Ï€ - bayesRiskPrior E P Ï€

lemma bayesRiskIncrease_comp (E : estimationProblem Î˜ ğ’´ ğ’µ) (P : kernel Î˜ ğ’³)
    (Ï€ : Measure Î˜) (Îº : kernel ğ’³ ğ’³') [IsMarkovKernel Îº] (Î· : kernel ğ’³' ğ’³'') [IsMarkovKernel Î·] :
    bayesRiskIncrease E P Ï€ (Î· âˆ˜â‚– Îº)
      = bayesRiskIncrease E P Ï€ Îº + bayesRiskIncrease E (Îº âˆ˜â‚– P) Ï€ Î· := by
  simp only [bayesRiskIncrease]
  rw [add_comm, tsub_add_tsub_cancel, kernel.comp_assoc]
  Â· exact bayesRiskPrior_le_bayesRiskPrior_comp _ _ _ _
  Â· exact bayesRiskPrior_le_bayesRiskPrior_comp _ _ _ _

lemma le_bayesRiskIncrease_comp (E : estimationProblem Î˜ ğ’´ ğ’µ) (P : kernel Î˜ ğ’³)
    (Ï€ : Measure Î˜) (Îº : kernel ğ’³ ğ’³') [IsMarkovKernel Îº] (Î· : kernel ğ’³' ğ’³'') [IsMarkovKernel Î·] :
    bayesRiskIncrease E (Îº âˆ˜â‚– P) Ï€ Î· â‰¤ bayesRiskIncrease E P Ï€ (Î· âˆ˜â‚– Îº) := by
  simp [bayesRiskIncrease_comp]

/-- **Data processing inequality** for the Bayes risk increase. -/
lemma bayesRiskIncrease_discard_comp_le_bayesRiskIncrease (E : estimationProblem Î˜ ğ’´ ğ’µ)
    (P : kernel Î˜ ğ’³) (Ï€ : Measure Î˜) (Îº : kernel ğ’³ ğ’³') [IsMarkovKernel Îº] :
    bayesRiskIncrease E (Îº âˆ˜â‚– P) Ï€ (kernel.discard ğ’³')
      â‰¤ bayesRiskIncrease E P Ï€ (kernel.discard ğ’³) := by
  convert le_bayesRiskIncrease_comp E P Ï€ Îº (kernel.discard ğ’³')
  simp

end ProbabilityTheory
