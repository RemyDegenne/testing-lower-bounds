/-
Copyright (c) 2024 RÃ©my Degenne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: RÃ©my Degenne
-/
import Mathlib.CategoryTheory.Monad.Kleisli
import TestingLowerBounds.Kernel.Category.CommutativeMonad

/-!

# Categories of measurable spaces and Kernels

-/

open CategoryTheory MonoidalCategory

universe u v

namespace CategoryTheory

section Kleisli

variable {C : Type u} [Category.{v} C] {T : Monad C}

@[reassoc (attr := simp)]
lemma Î·_naturality {X Y : C} (f : X âŸ¶ Y) : T.Î·.app X â‰« T.map f = f â‰« T.Î·.app Y := by
  simpa using (T.Î·.naturality f).symm

--@[reassoc (attr := simp)]
lemma Î¼_naturality {X Y : C} (f : X âŸ¶ Y) : T.map (T.map f) â‰« T.Î¼.app Y = T.Î¼.app X â‰« T.map f := by
  simpa using (T.Î¼.naturality f)

@[reassoc (attr := simp)]
lemma Î¼_naturality' {X Y : C} (f : X âŸ¶ Y) : T.Î¼.app X â‰« T.map f = T.map (T.map f) â‰« T.Î¼.app Y := by
  simpa using (T.Î¼.naturality f).symm

lemma Kleisli.comp_def {X Y Z : Kleisli T} (f : X âŸ¶ Y) (g : Y âŸ¶ Z) :
    f â‰« g = @CategoryStruct.comp C Category.toCategoryStruct _ _ _ f (T.map g) â‰« T.Î¼.app Z := by
  simp only [Category.assoc]
  rfl

variable [MonoidalCategory C]

instance (T : Monad C) [Strong T] : MonoidalCategoryStruct (Kleisli T) where
  tensorObj X Y := (Kleisli.Adjunction.toKleisli T).obj (X âŠ— Y)
  whiskerLeft X Yâ‚ Yâ‚‚ f :=
    ((T.Î·.app X âŠ— f) â‰« T.dStr X Yâ‚‚ : @tensorObj C _ _ X Yâ‚ âŸ¶ T.obj (X âŠ— Yâ‚‚))
  whiskerRight {Xâ‚ Xâ‚‚} f Y :=
    ((f âŠ— T.Î·.app Y) â‰« T.dStr Xâ‚‚ Y : @tensorObj C _ _ Xâ‚ Y âŸ¶ T.obj (Xâ‚‚ âŠ— Y))
  tensorUnit := (Kleisli.Adjunction.toKleisli T).obj (ğŸ™_ C)
  associator X Y Z := (Kleisli.Adjunction.toKleisli T).mapIso
    (@MonoidalCategoryStruct.associator C _ _ X Y Z)
  leftUnitor X := (Kleisli.Adjunction.toKleisli T).mapIso
    (@MonoidalCategoryStruct.leftUnitor C _ _ X)
  rightUnitor X := (Kleisli.Adjunction.toKleisli T).mapIso
    (@MonoidalCategoryStruct.rightUnitor C _ _ X)

@[simp]
lemma Kleisli.wiskerLeft_id [Strong T] {X Y : Kleisli T} :
    X â— ğŸ™ Y = ğŸ™ (X âŠ— Y) := by
  suffices (T.Î·.app X âŠ— T.Î·.app Y) â‰« T.dStr X Y = T.Î·.app (X âŠ— Y) from this
  simp

@[simp]
lemma Kleisli.id_whiskerRight [Strong T] {X Y : Kleisli T} :
    ğŸ™ X â–· Y = ğŸ™ (X âŠ— Y) := by
  suffices (T.Î·.app X âŠ— T.Î·.app Y) â‰« T.dStr X Y = T.Î·.app (X âŠ— Y) from this
  simp

lemma Kleisli.tensorObj_def [Strong T] (X Y : Kleisli T) :
    X âŠ— Y = @tensorObj C _ _ X Y := rfl

@[simp]
lemma Kleisli.tensorHom_def [Strong T]
    {Xâ‚ Yâ‚ Xâ‚‚ Yâ‚‚ : Kleisli T} (f : Xâ‚ âŸ¶ Yâ‚) (g: Xâ‚‚ âŸ¶ Yâ‚‚) :
    f âŠ— g = (f â–· Xâ‚‚) â‰« (Yâ‚ â— g) := rfl

lemma Kleisli.whiskerLeft_def [Strong T] (X : Kleisli T) {Yâ‚ Yâ‚‚ : Kleisli T} (f : Yâ‚ âŸ¶ Yâ‚‚) :
    X â— f = (T.Î·.app X âŠ— f) â‰« T.dStr X Yâ‚‚ := rfl

lemma Kleisli.whiskerRight_def [Strong T] {Xâ‚ Xâ‚‚ : Kleisli T} (f : Xâ‚ âŸ¶ Xâ‚‚) (Y : Kleisli T) :
    f â–· Y = ((f âŠ— T.Î·.app Y) â‰« T.dStr Xâ‚‚ Y : @tensorObj C _ _ Xâ‚ Y âŸ¶ T.obj (Xâ‚‚ âŠ— Y)) := rfl

lemma Kleisli.tensorUnit_def [Strong T] :
    ğŸ™_ (Kleisli T) = (Kleisli.Adjunction.toKleisli T).obj (ğŸ™_ C) := rfl

lemma Kleisli.associator_def [Strong T] (X Y Z : Kleisli T) :
    Î±_ X Y Z = (Kleisli.Adjunction.toKleisli T).mapIso
      (@MonoidalCategoryStruct.associator C _ _ X Y Z) := rfl

lemma Kleisli.leftUnitor_def [Strong T] (X : Kleisli T) :
    Î»_ X = (Kleisli.Adjunction.toKleisli T).mapIso
      (@MonoidalCategoryStruct.leftUnitor C _ _ X) := rfl

lemma Kleisli.rightUnitor_def [Strong T] (X : Kleisli T) :
    Ï_ X = (Kleisli.Adjunction.toKleisli T).mapIso
      (@MonoidalCategoryStruct.rightUnitor C _ _ X) := rfl

@[simp]
lemma Kleisli.whiskerLeft_comp [Strong T] (X : Kleisli T) {Yâ‚ Yâ‚‚ Yâ‚ƒ : Kleisli T}
    (f : Yâ‚ âŸ¶ Yâ‚‚) (g : Yâ‚‚ âŸ¶ Yâ‚ƒ) :
    X â— (f â‰« g) = X â— f â‰« X â— g := by
  simp [associator_def, whiskerRight_def, whiskerLeft_def, tensorObj_def, tensorUnit_def,
    leftUnitor_def, rightUnitor_def, comp_def]

@[simp]
lemma Kleisli.comp_whiskerRight [CommutativeMonad T] {Yâ‚ Yâ‚‚ Yâ‚ƒ : Kleisli T}
    (f : Yâ‚ âŸ¶ Yâ‚‚) (g : Yâ‚‚ âŸ¶ Yâ‚ƒ) (X : Kleisli T) :
    (f â‰« g) â–· X = f â–· X â‰« g â–· X := by
  simp [associator_def, whiskerRight_def, whiskerLeft_def, tensorObj_def, tensorUnit_def,
    leftUnitor_def, rightUnitor_def, comp_def]

lemma Kleisli.whisker_exchange [CommutativeMonad T] {W X Y Z : Kleisli T}
    (f : W âŸ¶ X) (g : Y âŸ¶ Z) :
    W â— g â‰« f â–· Z = f â–· Y â‰« X â— g := by
  simp only [tensorObj_def, whiskerLeft_def, Functor.id_obj, Monad.unit_dStr_left, whiskerRight_def,
    Monad.unit_dStr_right, comp_def, Functor.map_comp, Category.assoc]
  slice_rhs 2 3 => rw [â† T.rStr_naturality_id_left]
  slice_rhs 1 2 => rw [â† MonoidalCategory.whisker_exchange]
  slice_lhs 2 3 => rw [â† T.lStr_naturality_id_right]
  simp only [Category.assoc]
  congr 2
  exact T.lStr_rStr_comm X Z

namespace Kleisli

variable {W X Y Z Xâ‚ Yâ‚ Zâ‚ Xâ‚‚ Yâ‚‚ Zâ‚‚ Xâ‚ƒ Yâ‚ƒ Zâ‚ƒ : Kleisli T}

private lemma tensor_comp [CommutativeMonad T]
    (fâ‚ : Xâ‚ âŸ¶ Yâ‚) (fâ‚‚ : Xâ‚‚ âŸ¶ Yâ‚‚) (gâ‚ : Yâ‚ âŸ¶ Zâ‚) (gâ‚‚ : Yâ‚‚ âŸ¶ Zâ‚‚) :
    fâ‚ â‰« gâ‚ âŠ— fâ‚‚ â‰« gâ‚‚ = (fâ‚ âŠ— fâ‚‚) â‰« (gâ‚ âŠ— gâ‚‚) := by
  simp only [tensorHom_def, comp_whiskerRight, whiskerLeft_comp, Category.assoc]
  slice_lhs 2 3 => rw [â† whisker_exchange]
  simp

private lemma leftUnitor_naturality [CommutativeMonad T] (f : X âŸ¶ Y) :
    ğŸ™_ (Kleisli T) â— f â‰« (Î»_ Y).hom = (Î»_ X).hom â‰« f := by
  simp [associator_def, whiskerRight_def, whiskerLeft_def, tensorObj_def, tensorUnit_def,
    leftUnitor_def, rightUnitor_def, comp_def]

private lemma rightUnitor_naturality [CommutativeMonad T] (f : X âŸ¶ Y) :
    f â–· ğŸ™_ (Kleisli T) â‰« (Ï_ Y).hom = (Ï_ X).hom â‰« f := by
  simp [associator_def, whiskerRight_def, whiskerLeft_def, tensorObj_def, tensorUnit_def,
    rightUnitor_def, comp_def]

private lemma pentagon [CommutativeMonad T] (W X Y Z : Kleisli T) :
    (Î±_ W X Y).hom â–· Z â‰« (Î±_ W (X âŠ— Y) Z).hom â‰« W â— (Î±_ X Y Z).hom
      = (Î±_ (W âŠ— X) Y Z).hom â‰« (Î±_ W X (Y âŠ— Z)).hom := by
  simp [associator_def, whiskerRight_def, whiskerLeft_def, tensorObj_def, comp_def]

private lemma triangle [CommutativeMonad T] (X Y : Kleisli T) :
    (Î±_ X (ğŸ™_ (Kleisli T)) Y).hom â‰« X â— (Î»_ Y).hom = (Ï_ X).hom â–· Y := by
  simp [associator_def, whiskerRight_def, whiskerLeft_def, tensorObj_def,
    tensorUnit_def, leftUnitor_def, rightUnitor_def, comp_def]

@[reassoc (attr := simp)]
lemma rStr_rStr (X Y Z : C) [CommutativeMonad T] :
    T.rStr X Y â–· Z â‰« T.rStr (X âŠ— Y) Z
      = (Î±_ (T.obj X) Y Z).hom â‰« T.rStr X (Y âŠ— Z) â‰« T.map (Î±_ X Y Z).inv := by
  simp [Monad.rStr_assoc]

@[reassoc (attr := simp)]
lemma lStr_lStr (X Y Z : C) [CommutativeMonad T] :
    X â— T.lStr Y Z â‰« T.lStr X (Y âŠ— Z)
      = ((Î±_ X Y (T.obj Z)).inv â‰« T.lStr (X âŠ— Y) Z) â‰« T.map (Î±_ X Y Z).hom := by
   simp [Monad.lStr_assoc]

@[reassoc (attr := simp)]
lemma Monad.assoc_symm (T : Monad C) (Yâ‚ Yâ‚‚ Yâ‚ƒ : C) :
    T.Î¼.app (T.obj (Yâ‚ âŠ— Yâ‚‚ âŠ— Yâ‚ƒ)) â‰« T.Î¼.app (Yâ‚ âŠ— Yâ‚‚ âŠ— Yâ‚ƒ)
      = T.map (T.Î¼.app (Yâ‚ âŠ— Yâ‚‚ âŠ— Yâ‚ƒ)) â‰« T.Î¼.app (Yâ‚ âŠ— Yâ‚‚ âŠ— Yâ‚ƒ) := by
  rw [Monad.assoc]

private lemma associator_naturality [CommutativeMonad T]
    (fâ‚ : Xâ‚ âŸ¶ Yâ‚) (fâ‚‚ : Xâ‚‚ âŸ¶ Yâ‚‚) (fâ‚ƒ : Xâ‚ƒ âŸ¶ Yâ‚ƒ) :
    ((fâ‚ âŠ— fâ‚‚) âŠ— fâ‚ƒ) â‰« (Î±_ Yâ‚ Yâ‚‚ Yâ‚ƒ).hom = (Î±_ Xâ‚ Xâ‚‚ Xâ‚ƒ).hom â‰« (fâ‚ âŠ— fâ‚‚ âŠ— fâ‚ƒ) := by
  simp only [tensorObj_def, tensorHom_def, whiskerRight_def, Monad.unit_dStr_right, whiskerLeft_def,
    Functor.id_obj, Monad.unit_dStr_left, comp_def, Functor.map_comp, Category.assoc,
    MonoidalCategory.comp_whiskerRight, Monad.rStr_mul_comm, Monad.rStr_naturality_id_right_assoc,
    whisker_assoc, rStr_rStr_assoc, Iso.map_inv_hom_id_assoc, tensor_whiskerLeft,
    Î¼_naturality'_assoc, Î¼_naturality', associator_def, Functor.mapIso_hom,
    Adjunction.toKleisli_map, Monad.assoc_symm, Functor.comp_obj, whiskerRight_tensor,
    MonoidalCategory.whiskerLeft_comp, Monad.lStr_mul_comm, Monad.lStr_naturality_id_left_assoc,
    Î·_naturality_assoc, Î·_naturality, Iso.hom_inv_id_assoc, Monad.left_unit, Category.comp_id]
  congr 4
  simp_rw [â† Category.assoc]
  congr 2
  simp_rw [Category.assoc]
  simp_rw [â† T.map_comp]
  simp

instance [CommutativeMonad T] : MonoidalCategory (Kleisli T) where
  tensor_comp := Kleisli.tensor_comp
  associator_naturality := Kleisli.associator_naturality
  leftUnitor_naturality := Kleisli.leftUnitor_naturality
  rightUnitor_naturality := Kleisli.rightUnitor_naturality
  pentagon := Kleisli.pentagon
  triangle := Kleisli.triangle

def braiding [BraidedCategory C] [CommutativeMonad T] (X Y : Kleisli T) :
    X âŠ— Y â‰… Y âŠ— X :=
  (Adjunction.toKleisli T).mapIso (@BraidedCategory.braiding C _ _ _ X Y)

lemma braiding_def' [BraidedCategory C] [CommutativeMonad T] (X Y : Kleisli T) :
    Kleisli.braiding X Y
      = (Adjunction.toKleisli T).mapIso (@BraidedCategory.braiding C _ _ _ X Y) := rfl

private lemma braiding_naturality_right [SymmetricCategory C] [SymmetricMonad T]
    (X Y Z : Kleisli T) (f : Y âŸ¶ Z) :
    X â— f â‰« (X.braiding Z).hom = (X.braiding Y).hom â‰« f â–· X := by
  simp [braiding_def', associator_def, whiskerRight_def, whiskerLeft_def, tensorObj_def,
    tensorUnit_def, leftUnitor_def, rightUnitor_def, comp_def]

private lemma braiding_naturality_left [SymmetricCategory C] [SymmetricMonad T]
    (f : X âŸ¶ Y) (Z : Kleisli T) :
    f â–· Z â‰« (Y.braiding Z).hom = (X.braiding Z).hom â‰« Z â— f := by
  simp [braiding_def', associator_def, whiskerRight_def, whiskerLeft_def, tensorObj_def,
    tensorUnit_def, leftUnitor_def, rightUnitor_def, comp_def]

private lemma hexagon_forward [SymmetricCategory C] [SymmetricMonad T] (X Y Z : Kleisli T) :
    (Î±_ X Y Z).hom â‰« (X.braiding (Y âŠ— Z)).hom â‰« (Î±_ Y Z X).hom =
      (X.braiding Y).hom â–· Z â‰« (Î±_ Y X Z).hom â‰« Y â— (X.braiding Z).hom := by
  simp [braiding_def', associator_def, whiskerRight_def, whiskerLeft_def, tensorObj_def,
    tensorUnit_def, leftUnitor_def, rightUnitor_def, comp_def]

private lemma hexagon_reverse [SymmetricCategory C] [SymmetricMonad T] (X Y Z : Kleisli T) :
    (Î±_ X Y Z).inv â‰« ((X âŠ— Y).braiding Z).hom â‰« (Î±_ Z X Y).inv =
      X â— (Y.braiding Z).hom â‰« (Î±_ X Z Y).inv â‰« (X.braiding Z).hom â–· Y := by
  simp [braiding_def', associator_def, whiskerRight_def, whiskerLeft_def, tensorObj_def,
    tensorUnit_def, leftUnitor_def, rightUnitor_def, comp_def]

instance [SymmetricCategory C] [SymmetricMonad T] : BraidedCategory (Kleisli T) where
  braiding := Kleisli.braiding
  braiding_naturality_right := Kleisli.braiding_naturality_right
  braiding_naturality_left := Kleisli.braiding_naturality_left
  hexagon_forward := Kleisli.hexagon_forward
  hexagon_reverse := Kleisli.hexagon_reverse

lemma braiding_def [SymmetricCategory C] [SymmetricMonad T] (X Y : Kleisli T) :
    Î²_ X Y = (Adjunction.toKleisli T).mapIso (@BraidedCategory.braiding C _ _ _ X Y) := rfl

instance [SymmetricCategory C] [SymmetricMonad T] : SymmetricCategory (Kleisli T) where
  symmetry X Y := by
    simp only [tensorObj_def, braiding_def, Functor.mapIso_hom, Adjunction.toKleisli_map, comp_def,
      Functor.map_comp, Category.assoc, Î·_naturality_assoc, Î·_naturality,
      SymmetricCategory.symmetry_assoc, Monad.left_unit, Functor.id_obj, Category.comp_id]
    rfl

end Kleisli

end Kleisli

end CategoryTheory
