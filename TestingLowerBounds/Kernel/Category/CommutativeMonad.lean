/-
Copyright (c) 2024 RÃ©my Degenne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: RÃ©my Degenne
-/
import Mathlib.CategoryTheory.Monad.Basic
import Mathlib.CategoryTheory.Monoidal.Braided.Basic

/-!

# Categories of measurable spaces and Kernels

-/

open CategoryTheory MonoidalCategory

universe u v

namespace CategoryTheory

section CommutativeMonad

class LeftStrong {C : Type u} [Category.{v} C] [MonoidalCategory C] (T : Monad C) where
  leftStr : ((ğŸ­ C : C â¥¤ C).prod (T : C â¥¤ C)) â‹™ (tensor C) âŸ¶ (tensor C) â‹™ (T : C â¥¤ C)
  left_unit_comp (X : C) : (Î»_ (T.obj X)).inv â‰« leftStr.app (ğŸ™_ C, X)
      = T.map (Î»_ X).inv := by aesop_cat
  left_assoc (X Y Z : C) : leftStr.app (X âŠ— Y, Z) â‰« T.map (Î±_ X Y Z).hom
      = (Î±_ X Y (T.obj Z)).hom â‰« (X â— leftStr.app (Y, Z)) â‰« leftStr.app (X, Y âŠ— Z) := by
    aesop_cat
  left_unit_comm (X Y : C) : (X â— T.Î·.app Y) â‰« leftStr.app (X, Y) = T.Î·.app (X âŠ— Y) := by
    aesop_cat
  left_mul_comm (X Y : C) : (X â— T.Î¼.app Y) â‰« leftStr.app (X, Y)
      = leftStr.app (X, T.obj Y) â‰« T.map (leftStr.app (X, Y)) â‰« T.Î¼.app (X âŠ— Y) := by aesop_cat

class RightStrong {C : Type u} [Category.{v} C] [MonoidalCategory C] (T : Monad C) where
  rightStr : ((T : C â¥¤ C).prod (ğŸ­ C : C â¥¤ C)) â‹™ (tensor C) âŸ¶ (tensor C) â‹™ (T : C â¥¤ C)
  right_unit_comp (X : C) : (Ï_ (T.obj X)).inv â‰« rightStr.app (X, ğŸ™_ C)
      = T.map (Ï_ X).inv := by aesop_cat
  right_assoc (X Y Z : C) : rightStr.app (X, Y âŠ— Z) â‰« T.map (Î±_ X Y Z).inv
      = (Î±_ (T.obj X) Y Z).inv â‰« (rightStr.app (X, Y) â–· Z) â‰« rightStr.app (X âŠ— Y, Z) := by
    aesop_cat
  right_unit_comm (X Y : C) : (T.Î·.app X â–· Y) â‰« rightStr.app (X, Y) = T.Î·.app (X âŠ— Y) := by
    aesop_cat
  right_mul_comm (X Y : C) : (T.Î¼.app X â–· Y) â‰« rightStr.app (X, Y)
      = rightStr.app (T.obj X, Y) â‰« T.map (rightStr.app (X, Y)) â‰« T.Î¼.app (X âŠ— Y) := by aesop_cat

class Strong {C : Type u} [Category.{v} C] [MonoidalCategory C] (T : Monad C)
    extends LeftStrong T, RightStrong T where
  left_right (X Y Z : C) : (leftStr.app (X, Y) â–· Z) â‰« rightStr.app (X âŠ— Y, Z)
    = (Î±_ X (T.obj Y) Z).hom â‰« (X â— rightStr.app (Y, Z)) â‰« leftStr.app (X, Y âŠ— Z)
      â‰« T.map (Î±_ _ _ _).inv := by aesop_cat

class CommutativeMonad {C : Type u} [Category.{v} C] [MonoidalCategory C] (T : Monad C)
    extends Strong T where
  comm (X Y : C) : leftStr.app (T.obj X, Y) â‰« T.map (rightStr.app (X, Y)) â‰« T.Î¼.app (X âŠ— Y)
    = rightStr.app (X, T.obj Y) â‰« T.map (leftStr.app (X, Y)) â‰« T.Î¼.app (X âŠ— Y) := by aesop_cat

-- TODO: does that class make sense?
class SymmetricMonad {C : Type u} [Category.{v} C] [MonoidalCategory C]
    [SymmetricCategory C] (T : Monad C) extends CommutativeMonad T where
  braiding_left_right (X Y : C) : leftStr.app (X, Y) â‰« T.map (Î²_ X Y).hom
      = (Î²_ X (T.obj Y)).hom â‰« rightStr.app (Y, X) := by aesop_cat
  braiding_right_left (X Y : C) : rightStr.app (X, Y) â‰« T.map (Î²_ X Y).hom
      = (Î²_ (T.obj X) Y).hom â‰« leftStr.app (Y, X) := by aesop_cat

section LeftRightStrength

variable {C : Type u} [Category.{v} C] [MonoidalCategory C]

def Monad.lStr (T : Monad C) [LeftStrong T] (X Y : C) :
    X âŠ— T.obj Y âŸ¶ T.obj (X âŠ— Y) :=
  LeftStrong.leftStr.app (X, Y)

@[simp]
lemma Monad.lStr_unit_comp (T : Monad C) [LeftStrong T] (X : C) :
    (Î»_ (T.obj X)).inv â‰« T.lStr (ğŸ™_ C) X = T.map (Î»_ X).inv :=
  LeftStrong.left_unit_comp _

lemma Monad.lStr_assoc (T : Monad C) [LeftStrong T] (X Y Z : C) :
    T.lStr (X âŠ— Y) Z â‰« T.map (Î±_ X Y Z).hom
      = (Î±_ X Y (T.obj Z)).hom â‰« (X â— T.lStr Y Z) â‰« T.lStr X (Y âŠ— Z) :=
  LeftStrong.left_assoc _ _ _

@[simp]
lemma Monad.lStr_unit_comm (T : Monad C) [LeftStrong T] (X Y : C) :
    (X â— T.Î·.app Y) â‰« T.lStr X Y = T.Î·.app (X âŠ— Y) :=
  LeftStrong.left_unit_comm _ _

lemma Monad.lStr_mul_comm (T : Monad C) [LeftStrong T] (X Y : C) :
    (X â— T.Î¼.app Y) â‰« T.lStr X Y
      = T.lStr X (T.obj Y) â‰« T.map (T.lStr X Y) â‰« T.Î¼.app (X âŠ— Y) :=
  LeftStrong.left_mul_comm _ _

lemma Monad.lStr_naturality (T : Monad C) [LeftStrong T] {Xâ‚ Xâ‚‚ Yâ‚ Yâ‚‚ : C}
    (f : (Xâ‚, Xâ‚‚) âŸ¶ (Yâ‚, Yâ‚‚)) :
    (f.1 âŠ— T.map f.2) â‰« T.lStr Yâ‚ Yâ‚‚ = T.lStr Xâ‚ Xâ‚‚ â‰« T.map (f.1 âŠ— f.2) := by
  simpa using LeftStrong.leftStr.naturality _

lemma Monad.lStr_naturality' (T : Monad C) [LeftStrong T] {Xâ‚ Xâ‚‚ Yâ‚ Yâ‚‚ : C}
    (fâ‚ : Xâ‚ âŸ¶ Yâ‚) (fâ‚‚ : Xâ‚‚ âŸ¶ Yâ‚‚) :
    (fâ‚ âŠ— T.map fâ‚‚) â‰« T.lStr Yâ‚ Yâ‚‚ = T.lStr Xâ‚ Xâ‚‚ â‰« T.map (fâ‚ âŠ— fâ‚‚) := T.lStr_naturality (fâ‚, fâ‚‚)

lemma Monad.lStr_naturality_id_left (T : Monad C) [LeftStrong T] {X Yâ‚‚ : C} (Yâ‚ : C)
    (f : X âŸ¶ Yâ‚‚) :
    (Yâ‚ â— T.map f) â‰« T.lStr Yâ‚ Yâ‚‚ = T.lStr Yâ‚ X â‰« T.map (Yâ‚ â— f) := by
  simpa using T.lStr_naturality (ğŸ™ Yâ‚, f)

lemma Monad.lStr_naturality_id_right (T : Monad C) [LeftStrong T] {X Yâ‚ : C}
    (f : X âŸ¶ Yâ‚) (Yâ‚‚ : C) :
    (f â–· T.obj Yâ‚‚) â‰« T.lStr Yâ‚ Yâ‚‚ = T.lStr X Yâ‚‚ â‰« T.map (f â–· Yâ‚‚) := by
  simpa using T.lStr_naturality (f, ğŸ™ Yâ‚‚)

def Monad.rStr (T : Monad C) [RightStrong T] (X Y : C) :
    T.obj X âŠ— Y âŸ¶ T.obj (X âŠ— Y) :=
  RightStrong.rightStr.app (X, Y)

@[simp]
lemma Monad.rStr_unit_comp (T : Monad C) [RightStrong T] (X : C) :
    (Ï_ (T.obj X)).inv â‰« T.rStr X (ğŸ™_ C) = T.map (Ï_ X).inv :=
  RightStrong.right_unit_comp _

lemma Monad.rStr_assoc (T : Monad C) [RightStrong T] (X Y Z : C) :
    T.rStr X (Y âŠ— Z) â‰« T.map (Î±_ X Y Z).inv
      = (Î±_ (T.obj X) Y Z).inv â‰« (T.rStr X Y â–· Z) â‰« T.rStr (X âŠ— Y) Z :=
  RightStrong.right_assoc _ _ _

@[simp]
lemma Monad.rStr_unit_comm (T : Monad C) [RightStrong T] (X Y : C) :
    T.Î·.app X â–· Y â‰« T.rStr X Y = T.Î·.app (X âŠ— Y) :=
  RightStrong.right_unit_comm _ _

lemma Monad.rStr_mul_comm (T : Monad C) [RightStrong T] (X Y : C) :
    T.Î¼.app X â–· Y â‰« T.rStr X Y
      = T.rStr (T.obj X) Y â‰« T.map (T.rStr X Y) â‰« T.Î¼.app (X âŠ— Y) :=
  RightStrong.right_mul_comm _ _

lemma Monad.rStr_naturality (T : Monad C) [RightStrong T] {Xâ‚ Xâ‚‚ Yâ‚ Yâ‚‚ : C}
    (f : (Xâ‚, Xâ‚‚) âŸ¶ (Yâ‚, Yâ‚‚)) :
    (T.map f.1 âŠ— f.2) â‰« T.rStr Yâ‚ Yâ‚‚ = T.rStr Xâ‚ Xâ‚‚ â‰« T.map (f.1 âŠ— f.2) := by
  simpa using RightStrong.rightStr.naturality _

lemma Monad.rStr_naturality' (T : Monad C) [RightStrong T] {Xâ‚ Xâ‚‚ Yâ‚ Yâ‚‚ : C}
    (fâ‚ : Xâ‚ âŸ¶ Yâ‚) (fâ‚‚ : Xâ‚‚ âŸ¶ Yâ‚‚) :
    (T.map fâ‚ âŠ— fâ‚‚) â‰« T.rStr Yâ‚ Yâ‚‚ = T.rStr Xâ‚ Xâ‚‚ â‰« T.map (fâ‚ âŠ— fâ‚‚) := T.rStr_naturality (fâ‚, fâ‚‚)

lemma Monad.rStr_naturality_id_left (T : Monad C) [RightStrong T] {X Yâ‚‚ : C} (Yâ‚ : C)
    (f : X âŸ¶ Yâ‚‚) :
    (T.obj Yâ‚ â— f) â‰« T.rStr Yâ‚ Yâ‚‚ = T.rStr Yâ‚ X â‰« T.map (Yâ‚ â— f) := by
  simpa using T.rStr_naturality (ğŸ™ Yâ‚, f)

lemma Monad.rStr_naturality_id_right (T : Monad C) [RightStrong T] {X Yâ‚ : C}
    (f : X âŸ¶ Yâ‚) (Yâ‚‚ : C) :
    (T.map f â–· Yâ‚‚) â‰« T.rStr Yâ‚ Yâ‚‚ = T.rStr X Yâ‚‚ â‰« T.map (f â–· Yâ‚‚) := by
  simpa using T.rStr_naturality (f, ğŸ™ Yâ‚‚)

def Monad.dStr (T : Monad C) [Strong T] (X Y : C) :
    T.obj X âŠ— T.obj Y âŸ¶ T.obj (X âŠ— Y) :=
  (T.lStr (T.obj X) Y) â‰« T.map (T.rStr X Y) â‰« T.Î¼.app (X âŠ— Y)

lemma Monad.lStr_rStr (T : Monad C) [Strong T] (X Y Z : C) :
    (T.lStr X Y â–· Z) â‰« T.rStr (X âŠ— Y) Z
      = (Î±_ X (T.obj Y) Z).hom â‰« (X â— T.rStr Y Z) â‰« T.lStr X (Y âŠ— Z) â‰« T.map (Î±_ _ _ _).inv :=
  Strong.left_right _ _ _

lemma Monad.lStr_rStr_comm (T : Monad C) [CommutativeMonad T] (X Y : C) :
    T.lStr (T.obj X) Y â‰« T.map (T.rStr X Y) â‰« T.Î¼.app (X âŠ— Y)
      = T.rStr X (T.obj Y) â‰« T.map (T.lStr X Y) â‰« T.Î¼.app (X âŠ— Y) :=
  CommutativeMonad.comm _ _

lemma Monad.dStr_eq (T : Monad C) [CommutativeMonad T] (X Y : C) :
    T.dStr X Y = T.rStr X (T.obj Y) â‰« T.map (T.lStr X Y) â‰« T.Î¼.app (X âŠ— Y) :=
  T.lStr_rStr_comm X Y

@[simp]
lemma Monad.unit_whiskerRight_dStr (T : Monad C) [Strong T] (X Y : C) :
    (T.Î·.app X â–· T.obj Y) â‰« T.dStr X Y = T.lStr X Y := by
  simp only [dStr, Functor.id_obj]
  simp_rw [â† Category.assoc]
  rw [T.lStr_naturality_id_right (T.Î·.app X) Y]
  suffices (T.map (T.Î·.app X â–· Y) â‰« T.map (T.rStr X Y)) â‰« T.Î¼.app (X âŠ— Y) = ğŸ™ _ by simp [this]
  rw [â† T.map_comp]
  simp

@[simp]
lemma Monad.unit_whiskerLeft_dStr (T : Monad C) [CommutativeMonad T] (X Y : C) :
    (T.obj X â— T.Î·.app Y) â‰« T.dStr X Y = T.rStr X Y := by
  simp only [T.dStr_eq, Functor.id_obj]
  simp_rw [â† Category.assoc]
  rw [T.rStr_naturality_id_left X (T.Î·.app Y)]
  suffices (T.map (X â— T.Î·.app Y) â‰« T.map (T.lStr X Y)) â‰« T.Î¼.app (X âŠ— Y) = ğŸ™ _ by simp [this]
  rw [â† T.map_comp]
  simp

@[simp]
lemma Monad.unit_dStr_left (T : Monad C) [Strong T] (X : C) {Yâ‚ Yâ‚‚ : C}
    (f : Yâ‚ âŸ¶ T.obj Yâ‚‚) :
    (T.Î·.app X âŠ— f) â‰« T.dStr X Yâ‚‚ = X â— f â‰« T.lStr X Yâ‚‚ := by
  simp [tensorHom_def']

@[simp]
lemma Monad.unit_dStr_right (T : Monad C) [CommutativeMonad T] (X : C) {Yâ‚ Yâ‚‚ : C}
    (f : Yâ‚ âŸ¶ T.obj Yâ‚‚) :
    (f âŠ— T.Î·.app X) â‰« T.dStr Yâ‚‚ X = f â–· X â‰« T.rStr Yâ‚‚ X := by
  simp [tensorHom_def]

@[simp]
lemma Monad.lStr_comp_braiding (T : Monad C) [SymmetricCategory C] [SymmetricMonad T] (X Y : C) :
    T.lStr X Y â‰« T.map (Î²_ X Y).hom = (Î²_ X (T.obj Y)).hom â‰« T.rStr Y X :=
  SymmetricMonad.braiding_left_right _ _

@[simp]
lemma Monad.rStr_comp_braiding (T : Monad C) [SymmetricCategory C] [SymmetricMonad T] (X Y : C) :
    T.rStr X Y â‰« T.map (Î²_ X Y).hom = (Î²_ (T.obj X) Y).hom â‰« T.lStr Y X :=
  SymmetricMonad.braiding_right_left _ _

end LeftRightStrength

class Affine {C : Type u} [Category.{v} C] [MonoidalCategory C] (T : Monad C) where
  affine : T.obj (ğŸ™_ C) â‰… ğŸ™_ C := by aesop_cat

-- The Giry monad is not affine unless we restrict the measures to probability measures.

end CommutativeMonad

end CategoryTheory
