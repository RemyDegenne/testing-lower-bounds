/-
Copyright (c) 2024 RÃ©my Degenne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: RÃ©my Degenne, Lorenzo Luccioli
-/
import TestingLowerBounds.Divergences.fDivStatInfo

/-!

# Test: alternative f-divergence definition

-/

open Real MeasureTheory Filter Set MeasurableSpace

open scoped ENNReal NNReal Topology

namespace ProbabilityTheory

variable {Î± Î² : Type*} {m mÎ± : MeasurableSpace Î±} {mÎ² : MeasurableSpace Î²}
  {Î¼ Î½ : Measure Î±} {f g : â„ â†’ â„}

noncomputable
def rightLimZero (f : â„ â†’ â„) : EReal := Function.rightLim (fun x â†¦ (f x : EReal)) (0 : â„)

lemma rightLimZero_of_tendsto (h : Tendsto f (ğ“[>] 0) (ğ“ (f 0))) :
    rightLimZero f = f 0 := rightLim_eq_of_tendsto NeBot.ne' (EReal.tendsto_coe.mpr h)

lemma rightLimZero_of_tendsto_atTop (h : Tendsto f (ğ“[>] 0) atTop) :
    rightLimZero f = âŠ¤ := by
  refine rightLim_eq_of_tendsto NeBot.ne' ?_
  rw [EReal.tendsto_nhds_top_iff_real]
  rw [tendsto_atTop] at h
  intro x
  filter_upwards [h (x + 1)] with y hy
  norm_cast
  exact (lt_add_one x).trans_le hy

open Classical in
/-- f-Divergence of two measures. -/
noncomputable
def fDiv' (f : â„ â†’ â„) (Î¼ Î½ : Measure Î±) : EReal :=
  if Â¬ Integrable (fun x â†¦ f (Î¼.rnDeriv Î½ x).toReal) Î½ then âŠ¤
  else âˆ« x in (Î½.singularPartSet Î¼)á¶œ, f (Î¼.rnDeriv Î½ x).toReal âˆ‚Î½
    + derivAtTop f * Î¼.singularPart Î½ .univ + rightLimZero f * Î½.singularPart Î¼ univ

lemma fDiv'_eq_fDiv [SigmaFinite Î¼] [IsFiniteMeasure Î½] (hfc : ContinuousOn f (Ici 0)) :
    fDiv' f Î¼ Î½ = fDiv f Î¼ Î½ := by
  by_cases h_int : Integrable (fun x â†¦ f (Î¼.rnDeriv Î½ x).toReal) Î½
  swap; Â· rw [fDiv_of_not_integrable h_int, fDiv', if_pos h_int]
  have h_zero : rightLimZero f = f 0 := by
    refine rightLimZero_of_tendsto ?_
    have h_tendsto_ge : Tendsto f (ğ“[â‰¥] 0) (ğ“ (f 0)) := (hfc 0 (mem_Ici.mpr le_rfl)).tendsto
    exact tendsto_nhdsWithin_mono_left (Ioi_subset_Ici le_rfl) h_tendsto_ge
  rw [fDiv', if_neg, h_zero]
  swap; Â· simp [h_int]
  rw [fDiv_of_integrable h_int]
  suffices ((âˆ« x, f ((âˆ‚Î¼/âˆ‚Î½) x).toReal âˆ‚Î½ : â„) : EReal)
      = âˆ« x in (Î½.singularPartSet Î¼)á¶œ, f (Î¼.rnDeriv Î½ x).toReal âˆ‚Î½
        + f 0 * Î½.singularPart Î¼ .univ by
    rw [this, add_assoc, add_assoc]
    congr 1
    rw [add_comm]
  rw [â† integral_add_compl (Measure.measurableSet_singularPartSet (Î¼ := Î½) (Î½ := Î¼)) h_int,
    add_comm, EReal.coe_add]
  congr
  have h := Measure.rnDeriv_eq_zero_ae_of_singularPartSet Î½ Î¼ Î½
  rw [â† Measure.measure_singularPartSet' Î½ Î¼]
  have : âˆ« x in Î½.singularPartSet Î¼, f ((âˆ‚Î¼/âˆ‚Î½) x).toReal âˆ‚Î½
      = âˆ« _ in Î½.singularPartSet Î¼, f 0 âˆ‚Î½ := by
    refine setIntegral_congr_ae Measure.measurableSet_singularPartSet ?_
    filter_upwards [h] with x hx h_mem
    rw [hx h_mem, ENNReal.zero_toReal]
  rw [this]
  simp only [integral_const, MeasurableSet.univ, Measure.restrict_apply, univ_inter, smul_eq_mul,
    EReal.coe_mul]
  rw [mul_comm]
  congr
  refine EReal.coe_ennreal_toReal ?_
  simp

end ProbabilityTheory
