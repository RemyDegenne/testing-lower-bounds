/-
Copyright (c) 2024 RÃ©my Degenne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: RÃ©my Degenne, Lorenzo Luccioli
-/
import TestingLowerBounds.Divergences.StatInfo.fDivStatInfo

/-!
# fDiv and StatInfo

-/

open MeasureTheory Set

namespace ProbabilityTheory

variable {ğ’³ ğ’³' : Type*} {mğ’³ : MeasurableSpace ğ’³} {mğ’³' : MeasurableSpace ğ’³'}
  {Î¼ Î½ : Measure ğ’³} {f : â„ â†’ â„} {Î² Î³ x t : â„}

/-- **Data processing inequality** for the f-divergence of `statInfoFun`. -/
lemma fDiv_statInfoFun_comp_right_le [IsFiniteMeasure Î¼] [IsFiniteMeasure Î½]
    (Î· : Kernel ğ’³ ğ’³') [IsMarkovKernel Î·] (hÎ² : 0 â‰¤ Î²) :
    fDiv (statInfoFun Î² Î³) (Î· âˆ˜â‚˜ Î¼) (Î· âˆ˜â‚˜ Î½) â‰¤ fDiv (statInfoFun Î² Î³) Î¼ Î½ := by
  rcases le_total Î³ 0 with (hÎ³ | hÎ³)
  Â· rw [fDiv_statInfoFun_eq_zero_of_nonneg_of_nonpos hÎ² hÎ³,
      fDiv_statInfoFun_eq_zero_of_nonneg_of_nonpos hÎ² hÎ³]
  simp_rw [fDiv_statInfoFun_eq_StatInfo_of_nonneg hÎ² hÎ³]
  gcongr ?_ + ?_
  Â· exact EReal.coe_ennreal_le_coe_ennreal_iff.mpr <| statInfo_comp_le _ _ _ _
  Â· simp_rw [Measure.comp_apply_univ, le_refl]

-- The name is `fDiv_comp_right_le'`, since there is already `fDiv_comp_right_le`
-- in the `fDiv.CompProd` file.
/-- **Data processing inequality** for the f-divergence. -/
theorem fDiv_comp_right_le' [IsFiniteMeasure Î¼] [IsFiniteMeasure Î½]
    (Î· : Kernel ğ’³ ğ’³') [IsMarkovKernel Î·] (hf_cvx : ConvexOn â„ univ f) (hf_cont : Continuous f) :
    fDiv f (Î· âˆ˜â‚˜ Î¼) (Î· âˆ˜â‚˜ Î½) â‰¤ fDiv f Î¼ Î½ := by
  simp_rw [fDiv_eq_lintegral_fDiv_statInfoFun hf_cvx hf_cont, Measure.comp_apply_univ]
  gcongr
  simp only [EReal.coe_ennreal_le_coe_ennreal_iff]
  exact lintegral_mono fun x â†¦ EReal.toENNReal_le_toENNReal <|
    fDiv_statInfoFun_comp_right_le Î· zero_le_one

lemma fDiv_fst_le' (Î¼ Î½ : Measure (ğ’³ Ã— ğ’³')) [IsFiniteMeasure Î¼] [IsFiniteMeasure Î½]
    (hf_cvx : ConvexOn â„ univ f) (hf_cont : Continuous f) :
    fDiv f Î¼.fst Î½.fst â‰¤ fDiv f Î¼ Î½ := by
  simp_rw [Measure.fst, â† Measure.comp_deterministic_eq_map measurable_fst]
  exact fDiv_comp_right_le' _ hf_cvx hf_cont

lemma fDiv_snd_le' (Î¼ Î½ : Measure (ğ’³ Ã— ğ’³')) [IsFiniteMeasure Î¼] [IsFiniteMeasure Î½]
    (hf_cvx : ConvexOn â„ univ f) (hf_cont : Continuous f) :
    fDiv f Î¼.snd Î½.snd â‰¤ fDiv f Î¼ Î½ := by
  simp_rw [Measure.snd, â† Measure.comp_deterministic_eq_map measurable_snd]
  exact fDiv_comp_right_le' _ hf_cvx hf_cont

lemma le_fDiv_compProd' [IsFiniteMeasure Î¼] [IsFiniteMeasure Î½]
    (Îº Î· : Kernel ğ’³ ğ’³') [IsMarkovKernel Îº] [IsMarkovKernel Î·] (hf_cvx : ConvexOn â„ univ f)
    (hf_cont : Continuous f) :
    fDiv f Î¼ Î½ â‰¤ fDiv f (Î¼ âŠ—â‚˜ Îº) (Î½ âŠ—â‚˜ Î·) := by
  nth_rw 1 [â† Measure.fst_compProd Î¼ Îº, â† Measure.fst_compProd Î½ Î·]
  exact fDiv_fst_le' _ _ hf_cvx hf_cont

lemma fDiv_compProd_right' [IsFiniteMeasure Î¼] [IsFiniteMeasure Î½]
    (Îº : Kernel ğ’³ ğ’³') [IsMarkovKernel Îº] (hf_cvx : ConvexOn â„ univ f) (hf_cont : Continuous f) :
    fDiv f (Î¼ âŠ—â‚˜ Îº) (Î½ âŠ—â‚˜ Îº) = fDiv f Î¼ Î½ := by
  refine le_antisymm ?_ (le_fDiv_compProd' Îº Îº hf_cvx hf_cont)
  simp_rw [Measure.compProd_eq_comp]
  exact fDiv_comp_right_le' _ hf_cvx hf_cont

lemma fDiv_comp_le_compProd' [IsFiniteMeasure Î¼] [IsFiniteMeasure Î½]
    (Îº Î· : Kernel ğ’³ ğ’³') [IsMarkovKernel Îº] [IsMarkovKernel Î·] (hf_cvx : ConvexOn â„ univ f)
    (hf_cont : Continuous f) :
    fDiv f (Îº âˆ˜â‚˜ Î¼) (Î· âˆ˜â‚˜ Î½) â‰¤ fDiv f (Î¼ âŠ—â‚˜ Îº) (Î½ âŠ—â‚˜ Î·) := by
  nth_rw 1 [â† Measure.snd_compProd Î¼ Îº, â† Measure.snd_compProd Î½ Î·]
  exact fDiv_snd_le' _ _ hf_cvx hf_cont

lemma fDiv_comp_le_compProd_right' [IsFiniteMeasure Î¼]
    (Îº Î· : Kernel ğ’³ ğ’³') [IsMarkovKernel Îº] [IsMarkovKernel Î·] (hf_cvx : ConvexOn â„ univ f)
    (hf_cont : Continuous f) :
    fDiv f (Îº âˆ˜â‚˜ Î¼) (Î· âˆ˜â‚˜ Î¼) â‰¤ fDiv f (Î¼ âŠ—â‚˜ Îº) (Î¼ âŠ—â‚˜ Î·) :=
  fDiv_comp_le_compProd' Îº Î· hf_cvx hf_cont

end ProbabilityTheory
