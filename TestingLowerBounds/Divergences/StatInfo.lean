/-
Copyright (c) 2024 RÃ©my Degenne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: RÃ©my Degenne
-/
import TestingLowerBounds.CurvatureMeasure
import TestingLowerBounds.StatInfoFun
import Mathlib.Order.CompletePartialOrder
import TestingLowerBounds.FDiv.Basic
import TestingLowerBounds.Testing.Binary
import Mathlib.MeasureTheory.Constructions.Prod.Integral

/-!
# Statistical information

## Main definitions

* `statInfo`

## Main statements

* `statInfo_comp_le`: data-processing inequality

## Notation

## Implementation details

-/

open MeasureTheory Set

open scoped ENNReal NNReal

namespace ProbabilityTheory

variable {ğ’³ ğ’³' : Type*} {mğ’³ : MeasurableSpace ğ’³} {mğ’³' : MeasurableSpace ğ’³'}
  {Î¼ Î½ : Measure ğ’³} {p : â„â‰¥0âˆ} {Ï€ : Measure Bool}

-- TODO: replace the min by a risk
/-- The statistical information of the measures `Î¼` and `Î½` with respect to
the prior `Ï€ âˆˆ â„³({0,1})`. -/
noncomputable
def statInfo (Î¼ Î½ : Measure ğ’³) (Ï€ : Measure Bool) : â„â‰¥0âˆ :=
  bayesBinaryRisk (Î¼ âˆ˜â‚˜ kernel.discard ğ’³) (Î½ âˆ˜â‚˜ kernel.discard ğ’³) Ï€ - bayesBinaryRisk Î¼ Î½ Ï€

lemma statInfo_eq_min_sub (Î¼ Î½ : Measure ğ’³) (Ï€ : Measure Bool) :
    statInfo Î¼ Î½ Ï€ = min (Ï€ {false} * Î¼ univ) (Ï€ {true} * Î½ univ) - bayesBinaryRisk Î¼ Î½ Ï€ := by
  simp_rw [statInfo, Measure.comp_discard, bayesBinaryRisk_dirac]

lemma statInfo_eq_bayesRiskIncrease (Î¼ Î½ : Measure ğ’³) (Ï€ : Measure Bool) :
    statInfo Î¼ Î½ Ï€ = bayesRiskIncrease (simpleBinaryHypTest Î¼ Î½) Ï€ (kernel.discard ğ’³) := by
  simp_rw [statInfo, bayesBinaryRisk, bayesRiskIncrease, simpleBinaryHypTest_comp]

/-- **Data processing inequality** for the statistical information. -/
lemma statInfo_comp_le (Î¼ Î½ : Measure ğ’³) (Ï€ : Measure Bool) (Î· : kernel ğ’³ ğ’³') [IsMarkovKernel Î·] :
    statInfo (Î¼ âˆ˜â‚˜ Î·) (Î½ âˆ˜â‚˜ Î·) Ï€ â‰¤ statInfo Î¼ Î½ Ï€ := by
  simp_rw [statInfo_eq_min_sub, Measure.comp_apply_univ]
  refine tsub_le_tsub ?_ (bayesBinaryRisk_le_bayesBinaryRisk_comp _ _ _ _)
  simp

@[simp]
lemma statInfo_self (Î¼ : Measure ğ’³) (Ï€ : Measure Bool) : statInfo Î¼ Î¼ Ï€ = 0 := by
  simp_rw [statInfo, bayesBinaryRisk_self, Measure.comp_apply_univ, tsub_self]

lemma toReal_statInfo_eq_toReal_sub [IsFiniteMeasure Î½] [IsFiniteMeasure Ï€] :
    (statInfo Î¼ Î½ Ï€).toReal = (min (Ï€ {false} * Î¼ univ) (Ï€ {true} * Î½ univ)).toReal
      - (bayesBinaryRisk Î¼ Î½ Ï€).toReal := by
  rw [statInfo_eq_min_sub, ENNReal.toReal_sub_of_le]
  Â· exact bayesBinaryRisk_le_min _ _ _
  Â· simp only [ne_eq, min_eq_top, not_and]
    exact fun _ â†¦  ENNReal.mul_ne_top (measure_ne_top Ï€ _) (measure_ne_top Î½ _)

lemma statInfo_le_min : statInfo Î¼ Î½ Ï€ â‰¤ min (Ï€ {false} * Î¼ univ) (Ï€ {true} * Î½ univ) :=
  statInfo_eq_min_sub Î¼ Î½ Ï€ â–¸ tsub_le_self

lemma statInfo_symm : statInfo Î¼ Î½ Ï€ = statInfo Î½ Î¼ (Ï€.map Bool.not) := by
  simp_rw [statInfo, bayesBinaryRisk_symm _ _ Ï€]

lemma statInfo_boolMeasure_le_statInfo {E : Set ğ’³} (hE : MeasurableSet E) :
    statInfo (Bool.boolMeasure (Î¼ Eá¶œ) (Î¼ E)) (Bool.boolMeasure (Î½ Eá¶œ) (Î½ E)) Ï€
      â‰¤ statInfo Î¼ Î½ Ï€ := by
  have h_meas : Measurable fun x â†¦ Bool.ofNat (E.indicator 1 x) :=
    ((measurable_discrete _).comp' (measurable_one.indicator hE))
  let Î· : kernel ğ’³ Bool := kernel.deterministic (fun x â†¦ Bool.ofNat (E.indicator 1 x)) h_meas
  have h_false : (fun x â†¦ Bool.ofNat (E.indicator 1 x)) â»Â¹' {false} = Eá¶œ := by
    ext x; simp [Bool.ofNat]
  have h_true : (fun x â†¦ Bool.ofNat (E.indicator 1 x)) â»Â¹' {true} = E := by
    ext x; simp [Bool.ofNat]
  convert statInfo_comp_le Î¼ Î½ Ï€ Î· <;>
  Â· ext
    Â· rw [Measure.comp_deterministic_eq_map, Measure.map_apply h_meas (by trivial), h_false,
        Bool.boolMeasure_apply_false]
    Â· rw [Measure.comp_deterministic_eq_map, Measure.map_apply h_meas (by trivial), h_true,
        Bool.boolMeasure_apply_true]

lemma statInfo_eq_min_sub_lintegral (Î¼ Î½ : Measure ğ’³) [IsFiniteMeasure Î¼] [IsFiniteMeasure Î½]
    (Ï€ : Measure Bool) [IsFiniteMeasure Ï€] :
    statInfo Î¼ Î½ Ï€ = min (Ï€ {false} * Î¼ univ) (Ï€ {true} * Î½ univ)
      - âˆ«â» x, min (Ï€ {false} * Î¼.rnDeriv (Ï€ âˆ˜â‚˜ twoHypKernel Î¼ Î½) x)
      (Ï€ {true} * Î½.rnDeriv (Ï€ âˆ˜â‚˜ twoHypKernel Î¼ Î½) x) âˆ‚(Ï€ âˆ˜â‚˜ twoHypKernel Î¼ Î½) := by
  rw [statInfo_eq_min_sub, bayesBinaryRisk_eq_lintegral_min]

lemma statInfo_eq_min_sub_lintegral' {Î¼ Î½ Î¶ : Measure ğ’³} [IsFiniteMeasure Î¼] [IsFiniteMeasure Î½]
    [SigmaFinite Î¶] (Ï€ : Measure Bool) [IsFiniteMeasure Ï€] (hÎ¼Î¶ : Î¼ â‰ª Î¶) (hÎ½Î¶ : Î½ â‰ª Î¶) :
    statInfo Î¼ Î½ Ï€ = min (Ï€ {false} * Î¼ univ) (Ï€ {true} * Î½ univ)
      - âˆ«â» x, min (Ï€ {false} * (âˆ‚Î¼/âˆ‚Î¶) x) (Ï€ {true} * (âˆ‚Î½/âˆ‚Î¶) x) âˆ‚Î¶ := by
  by_cases h_false : Ï€ {false} = 0
  Â· simp [statInfo, h_false, bayesBinaryRisk_of_measure_false_eq_zero]
  by_cases h_true : Ï€ {true} = 0
  Â· simp [statInfo, h_true, bayesBinaryRisk_of_measure_true_eq_zero]
  have hÎ¼ac : Î¼ â‰ª (Ï€ âˆ˜â‚˜ twoHypKernel Î¼ Î½) :=
    absolutelyContinuous_measure_comp_twoHypKernel_left Î¼ Î½ h_false
  have hÎ½ac : Î½ â‰ª (Ï€ âˆ˜â‚˜ twoHypKernel Î¼ Î½) :=
    absolutelyContinuous_measure_comp_twoHypKernel_right Î¼ Î½ h_true
  have hacÎ¶ : (Ï€ âˆ˜â‚˜ twoHypKernel Î¼ Î½) â‰ª Î¶ :=
    measure_comp_twoHypKernel _ _ _ â–¸ (hÎ½Î¶.smul _).add_left (hÎ¼Î¶.smul _)
  have hÎ¼ := Measure.rnDeriv_mul_rnDeriv hÎ¼ac (Îº := Î¶)
  have hÎ½ := Measure.rnDeriv_mul_rnDeriv hÎ½ac (Îº := Î¶)
  rw [statInfo_eq_min_sub_lintegral, â† lintegral_rnDeriv_mul hacÎ¶ (by fun_prop)]
  congr 1
  apply lintegral_congr_ae
  filter_upwards [hÎ¼, hÎ½] with x hxÎ¼ hxÎ½
  rw [ENNReal.mul_min, mul_comm, mul_comm _ (Ï€ _ * _), mul_assoc, mul_assoc]
  congr

lemma toReal_statInfo_eq_min_sub_integral (Î¼ Î½ : Measure ğ’³) [IsFiniteMeasure Î¼] [IsFiniteMeasure Î½]
    (Ï€ : Measure Bool) [IsFiniteMeasure Ï€] :
    (statInfo Î¼ Î½ Ï€).toReal = min (Ï€ {false} * Î¼ univ).toReal (Ï€ {true} * Î½ univ).toReal
      - âˆ« x, min (Ï€ {false} * Î¼.rnDeriv (Ï€ âˆ˜â‚˜ twoHypKernel Î¼ Î½) x).toReal
      (Ï€ {true} * Î½.rnDeriv (Ï€ âˆ˜â‚˜ twoHypKernel Î¼ Î½) x).toReal âˆ‚(Ï€ âˆ˜â‚˜ twoHypKernel Î¼ Î½) := by
  have hÎ¼ : Ï€ {false} * Î¼ univ â‰  âŠ¤ := ENNReal.mul_ne_top (measure_ne_top Ï€ _) (measure_ne_top Î¼ _)
  have hÎ½ : Ï€ {true} * Î½ univ â‰  âŠ¤ := ENNReal.mul_ne_top (measure_ne_top Ï€ _) (measure_ne_top Î½ _)
  rw [statInfo_eq_min_sub, ENNReal.toReal_sub_of_le (bayesBinaryRisk_le_min Î¼ Î½ Ï€)]
  swap; Â· simp only [ne_eq, min_eq_top, hÎ¼, hÎ½, and_self, not_false_eq_true]
  rw [toReal_bayesBinaryRisk_eq_integral_min,
    MonotoneOn.map_min (fun _ _ _ hb hab â†¦ ENNReal.toReal_mono hb hab) hÎ¼ hÎ½]

section StatInfoFun

open Set Filter ConvexOn

variable {ğ’³ : Type*} {mğ’³ : MeasurableSpace ğ’³} {Î¼ Î½ : Measure ğ’³} {f : â„ â†’ â„} {Î² Î³ x t : â„}

lemma integrable_statInfoFun_rnDeriv (Î² Î³ : â„)
    (Î¼ Î½ : Measure ğ’³) [IsFiniteMeasure Î¼] [IsFiniteMeasure Î½] :
    Integrable (fun x â†¦ statInfoFun Î² Î³ ((âˆ‚Î¼/âˆ‚Î½) x).toReal) Î½ := by
  refine integrable_f_rnDeriv_of_derivAtTop_ne_top _ _ stronglyMeasurable_statInfoFun3
    ?_ (derivAtTop_statInfoFun_ne_top Î² Î³)
  exact (convexOn_statInfoFun Î² Î³).subset (fun _ _ â†¦ trivial) (convex_Ici 0)

lemma nnReal_mul_fDiv {a : NNReal} :
    a * fDiv (statInfoFun Î² Î³) Î¼ Î½ = fDiv (fun x â†¦ statInfoFun (a * Î²) (a * Î³) x) Î¼ Î½ := by
  change (a.1 : EReal) * _ = _
  rw [â† fDiv_mul a.2 ((convexOn_statInfoFun Î² Î³).subset (fun _ _ â†¦ trivial) (convex_Ici 0)) Î¼ Î½]
  simp_rw [const_mul_statInfoFun a.2]
  rfl

lemma fDiv_statInfoFun_eq_integral_max_of_nonneg_of_le [IsFiniteMeasure Î¼] [IsFiniteMeasure Î½]
    (hÎ² : 0 â‰¤ Î²) (hÎ³ : Î³ â‰¤ Î²) :
    fDiv (statInfoFun Î² Î³) Î¼ Î½ = âˆ« x, max 0 (Î³ - Î² * ((âˆ‚Î¼/âˆ‚Î½) x).toReal) âˆ‚Î½ := by
  simp_rw [fDiv_of_integrable (integrable_statInfoFun_rnDeriv _ _ _ _),
    derivAtTop_statInfoFun_of_nonneg_of_le hÎ² hÎ³, zero_mul, add_zero, statInfoFun_of_le hÎ³]

lemma fDiv_statInfoFun_eq_integral_max_of_nonneg_of_gt [IsFiniteMeasure Î¼] [IsFiniteMeasure Î½]
    (hÎ² : 0 â‰¤ Î²) (hÎ³ : Î² < Î³) :
    fDiv (statInfoFun Î² Î³) Î¼ Î½
      = âˆ« x, max 0 (Î² * ((âˆ‚Î¼/âˆ‚Î½) x).toReal - Î³) âˆ‚Î½ + Î² * (Î¼.singularPart Î½) univ := by
  simp_rw [fDiv_of_integrable (integrable_statInfoFun_rnDeriv _ _ _ _),
    derivAtTop_statInfoFun_of_nonneg_of_gt hÎ² hÎ³, statInfoFun_of_gt hÎ³]

lemma fDiv_statInfoFun_eq_integral_max_of_nonpos_of_le [IsFiniteMeasure Î¼] [IsFiniteMeasure Î½]
    (hÎ² : Î² â‰¤ 0) (hÎ³ : Î³ â‰¤ Î²) :
    fDiv (statInfoFun Î² Î³) Î¼ Î½
      = âˆ« x, max 0 (Î³ - Î² * ((âˆ‚Î¼/âˆ‚Î½) x).toReal) âˆ‚Î½ - Î² * (Î¼.singularPart Î½) univ := by
  simp_rw [fDiv_of_integrable (integrable_statInfoFun_rnDeriv _ _ _ _),
    derivAtTop_statInfoFun_of_nonpos_of_le hÎ² hÎ³, statInfoFun_of_le hÎ³, neg_mul, â† sub_eq_add_neg]

lemma fDiv_statInfoFun_eq_integral_max_of_nonpos_of_gt [IsFiniteMeasure Î¼] [IsFiniteMeasure Î½]
    (hÎ² : Î² â‰¤ 0) (hÎ³ : Î² < Î³) :
    fDiv (statInfoFun Î² Î³) Î¼ Î½ = âˆ« x, max 0 (Î² * ((âˆ‚Î¼/âˆ‚Î½) x).toReal - Î³) âˆ‚Î½ := by
  simp_rw [fDiv_of_integrable (integrable_statInfoFun_rnDeriv _ _ _ _),
    derivAtTop_statInfoFun_of_nonpos_of_gt hÎ² hÎ³, statInfoFun_of_gt hÎ³, zero_mul, add_zero]

/-- Auxiliary lemma for `fDiv_statInfoFun_eq_integral_abs_of_nonneg_of_le` and
`fDiv_statInfoFun_eq_integral_abs_of_nonpos_of_le`. -/
lemma integral_max_eq_integral_abs [IsFiniteMeasure Î¼] [IsFiniteMeasure Î½] :
    âˆ« x, max 0 (Î³ - Î² * ((âˆ‚Î¼/âˆ‚Î½) x).toReal) âˆ‚Î½
      = 2â»Â¹ * (âˆ« x, |Î² * ((âˆ‚Î¼/âˆ‚Î½) x).toReal - Î³| âˆ‚Î½ + Î³ * (Î½ univ).toReal - Î² * (Î¼ univ).toReal
        + Î² * ((Î¼.singularPart Î½) univ).toReal) := by
  simp_rw [max_eq_add_add_abs_sub, zero_add, zero_sub, neg_sub, integral_mul_left]
  congr
  have h_int : Integrable (fun x â†¦ Î² * ((âˆ‚Î¼/âˆ‚Î½) x).toReal) Î½ :=
    Measure.integrable_toReal_rnDeriv.const_mul _
  have h_int' : Integrable (fun x â†¦ Î³ - Î² * ((âˆ‚Î¼/âˆ‚Î½) x).toReal) Î½ := (integrable_const Î³).sub h_int
  rw [integral_add h_int', integral_sub (integrable_const Î³) h_int, integral_const, smul_eq_mul,
    mul_comm, integral_mul_left, add_comm, add_sub_assoc, add_assoc, sub_eq_add_neg, sub_eq_add_neg,
    add_assoc, â† mul_neg, â† mul_neg, â† mul_add]
  swap; Â· exact (integrable_add_const_iff.mpr h_int).abs
  congr
  nth_rw 2 [Measure.haveLebesgueDecomposition_add Î¼ Î½]
  simp only [Measure.coe_add, Pi.add_apply, MeasurableSet.univ, withDensity_apply,
    Measure.restrict_univ]
  rw [ENNReal.toReal_add (measure_ne_top _ _)]
  swap; Â· exact lt_top_iff_ne_top.mp <| (setLIntegral_univ _ â–¸
    Measure.setLIntegral_rnDeriv_le univ).trans_lt IsFiniteMeasure.measure_univ_lt_top
  ring_nf
  rw [integral_toReal (Measure.measurable_rnDeriv Î¼ Î½).aemeasurable (Measure.rnDeriv_lt_top Î¼ Î½)]

/-- Auxiliary lemma for `fDiv_statInfoFun_eq_integral_abs_of_nonneg_of_gt` and
`fDiv_statInfoFun_eq_integral_abs_of_nonpos_of_gt`. -/
lemma integral_max_eq_integral_abs' [IsFiniteMeasure Î¼] [IsFiniteMeasure Î½] :
    âˆ« x, max 0 (Î² * ((âˆ‚Î¼/âˆ‚Î½) x).toReal - Î³) âˆ‚Î½
      = 2â»Â¹ * (âˆ« x, |Î² * ((âˆ‚Î¼/âˆ‚Î½) x).toReal - Î³| âˆ‚Î½ - Î³ * (Î½ univ).toReal + Î² * (Î¼ univ).toReal
        - Î² * ((Î¼.singularPart Î½) univ).toReal) := by
  simp_rw [max_eq_add_add_abs_sub, zero_add, zero_sub, abs_neg, integral_mul_left]
  congr
  have h_int : Integrable (fun x â†¦ Î² * ((âˆ‚Î¼/âˆ‚Î½) x).toReal) Î½ :=
    Measure.integrable_toReal_rnDeriv.const_mul _
  have h_int' : Integrable (fun x â†¦ Î² * ((âˆ‚Î¼/âˆ‚Î½) x).toReal - Î³) Î½ := h_int.sub (integrable_const Î³)
  rw [integral_add h_int', integral_sub h_int (integrable_const Î³), integral_const, smul_eq_mul,
    mul_comm, integral_mul_left, add_comm, add_sub_assoc, sub_eq_add_neg, add_comm (Î² * _),
    â† add_assoc, â† sub_eq_add_neg]
  swap; Â· exact (h_int.sub (integrable_const _)).abs
  congr
  nth_rw 2 [Measure.haveLebesgueDecomposition_add Î¼ Î½]
  simp only [Measure.coe_add, Pi.add_apply, MeasurableSet.univ, withDensity_apply,
    Measure.restrict_univ]
  rw [ENNReal.toReal_add (measure_ne_top _ _)]
  swap; Â· exact lt_top_iff_ne_top.mp <| (setLIntegral_univ _ â–¸
    Measure.setLIntegral_rnDeriv_le univ).trans_lt IsFiniteMeasure.measure_univ_lt_top
  ring_nf
  rw [integral_toReal (Measure.measurable_rnDeriv Î¼ Î½).aemeasurable (Measure.rnDeriv_lt_top Î¼ Î½)]

lemma fDiv_statInfoFun_eq_integral_abs_of_nonneg_of_le [IsFiniteMeasure Î¼] [IsFiniteMeasure Î½]
    (hÎ² : 0 â‰¤ Î²) (hÎ³ : Î³ â‰¤ Î²) :
    fDiv (statInfoFun Î² Î³) Î¼ Î½ = (2 : â„)â»Â¹ * (âˆ« x, |Î² * ((âˆ‚Î¼/âˆ‚Î½) x).toReal - Î³| âˆ‚Î½
      + Î² * (Î¼.singularPart Î½) univ + Î³ * Î½ univ - Î² * Î¼ univ) := by
  rw [fDiv_statInfoFun_eq_integral_max_of_nonneg_of_le hÎ² hÎ³, integral_max_eq_integral_abs,
    sub_eq_add_neg, add_assoc, add_comm (- _), â† add_assoc, â† sub_eq_add_neg, add_assoc,
    add_comm (_ * _), add_assoc]
  simp only [EReal.coe_mul, EReal.coe_sub, EReal.coe_add,
    EReal.coe_ennreal_toReal (measure_ne_top _ _)]

lemma fDiv_statInfoFun_eq_integral_abs_of_nonneg_of_gt [IsFiniteMeasure Î¼] [IsFiniteMeasure Î½]
    (hÎ² : 0 â‰¤ Î²) (hÎ³ : Î² < Î³) :
    fDiv (statInfoFun Î² Î³) Î¼ Î½ = (2 : â„)â»Â¹ * (âˆ« x, |Î² * ((âˆ‚Î¼/âˆ‚Î½) x).toReal - Î³| âˆ‚Î½
      + Î² * (Î¼.singularPart Î½) univ + Î² * Î¼ univ - Î³ * Î½ univ) := by
  have h_eq :
      (Î² : EReal) * ((Î¼.singularPart Î½) univ)
        = â†‘(2â»Â¹ * (2 * Î² * ((Î¼.singularPart Î½) univ).toReal)) := by
    simp [mul_assoc, EReal.coe_ennreal_toReal (measure_ne_top _ _)]
  rw [fDiv_statInfoFun_eq_integral_max_of_nonneg_of_gt hÎ² hÎ³, integral_max_eq_integral_abs', h_eq,
    â† EReal.coe_add, â† mul_add, EReal.coe_mul]
  simp_rw [â† EReal.coe_ennreal_toReal (measure_ne_top _ _), â† EReal.coe_mul, sub_eq_add_neg,
    â† EReal.coe_neg, â† EReal.coe_add, add_assoc]
  congr 3
  ring

lemma fDiv_statInfoFun_eq_integral_abs_of_nonpos_of_le [IsFiniteMeasure Î¼] [IsFiniteMeasure Î½]
    (hÎ² : Î² â‰¤ 0) (hÎ³ : Î³ â‰¤ Î²) :
    fDiv (statInfoFun Î² Î³) Î¼ Î½ = (2 : â„)â»Â¹ * (âˆ« x, |Î² * ((âˆ‚Î¼/âˆ‚Î½) x).toReal - Î³| âˆ‚Î½
      - Î² * (Î¼.singularPart Î½) univ + Î³ * Î½ univ - Î² * Î¼ univ) := by
  have h_eq :
      (Î² : EReal) * ((Î¼.singularPart Î½) univ)
        = â†‘(2â»Â¹ * (2 * Î² * ((Î¼.singularPart Î½) univ).toReal)) := by
    simp [mul_assoc, EReal.coe_ennreal_toReal (measure_ne_top _ _)]
  rw [fDiv_statInfoFun_eq_integral_max_of_nonpos_of_le hÎ² hÎ³, integral_max_eq_integral_abs, h_eq,
    sub_eq_add_neg, â† EReal.coe_neg, â† EReal.coe_add, â† mul_neg, â† mul_add, EReal.coe_mul]
  simp_rw [â† EReal.coe_ennreal_toReal (measure_ne_top _ _), â† EReal.coe_mul, sub_eq_add_neg,
    â† EReal.coe_neg, â† EReal.coe_add, add_assoc]
  congr 3
  ring

lemma fDiv_statInfoFun_eq_integral_abs_of_nonpos_of_gt [IsFiniteMeasure Î¼] [IsFiniteMeasure Î½]
    (hÎ² : Î² â‰¤ 0) (hÎ³ : Î² < Î³) :
    fDiv (statInfoFun Î² Î³) Î¼ Î½ = (2 : â„)â»Â¹ * (âˆ« x, |Î² * ((âˆ‚Î¼/âˆ‚Î½) x).toReal - Î³| âˆ‚Î½
      - Î² * (Î¼.singularPart Î½) univ + Î² * Î¼ univ - Î³ * Î½ univ) := by
  rw [fDiv_statInfoFun_eq_integral_max_of_nonpos_of_gt hÎ² hÎ³, integral_max_eq_integral_abs']
  simp_rw [â† EReal.coe_ennreal_toReal (measure_ne_top _ _), â† EReal.coe_mul, sub_eq_add_neg,
    â† EReal.coe_neg, â† EReal.coe_add, â† EReal.coe_mul]
  ring_nf

lemma integral_statInfoFun_curvatureMeasure (hf_cvx : ConvexOn â„ univ f)
    (hf_cont : Continuous f) (hf_one : f 1 = 0) (hfderiv_one : rightDeriv f 1 = 0) :
    âˆ« y, statInfoFun 1 y t âˆ‚(curvatureMeasure hf_cvx) = f t := by
  have h :
      f t - f 1 - (rightDeriv f 1) * (t - 1) = âˆ« x in (1)..t, t - x âˆ‚(curvatureMeasure hf_cvx) :=
    convex_taylor hf_cvx hf_cont
  rw [hf_one, hfderiv_one, sub_zero, zero_mul, sub_zero] at h
  rw [h]
  rcases le_total t 1 with (ht | ht)
  Â· simp_rw [statInfoFun_of_one_of_right_le_one ht, integral_indicator measurableSet_Ioc,
      intervalIntegral.integral_of_ge ht, â† integral_neg, neg_sub]
  Â· simp_rw [statInfoFun_of_one_of_one_le_right ht, integral_indicator measurableSet_Ioc,
      intervalIntegral.integral_of_le ht]

-- TODO: think about the case when the function is not integrable (`h_int`).
-- Can we prove that in this case the rhs is also not integrable?
lemma fDiv_eq_integral_fDiv_statInfoFun_of_absolutelyContinuous
    [IsFiniteMeasure Î¼] [IsFiniteMeasure Î½]
    (hf_cvx : ConvexOn â„ univ f) (hf_cont : Continuous f) (hf_one : f 1 = 0)
    (hfderiv_one : rightDeriv f 1 = 0) (h_int : Integrable (fun x â†¦ f ((âˆ‚Î¼/âˆ‚Î½) x).toReal) Î½)
    (h_ac : Î¼ â‰ª Î½) :
    fDiv f Î¼ Î½ = âˆ« x, (fDiv (statInfoFun 1 x) Î¼ Î½).toReal âˆ‚(curvatureMeasure hf_cvx) := by
  classical
  rw [fDiv_of_absolutelyContinuous h_ac, if_pos h_int, EReal.coe_eq_coe_iff]
  simp_rw [fDiv_of_absolutelyContinuous h_ac, if_pos (integrable_statInfoFun_rnDeriv 1 _ _ _),
    EReal.toReal_coe,
    â† integral_statInfoFun_curvatureMeasure hf_cvx hf_cont hf_one hfderiv_one]
  have h_meas : Measurable (fun x Î³ â†¦ statInfoFun 1 Î³ ((âˆ‚Î¼/âˆ‚Î½) x).toReal).uncurry := by
    change Measurable
      (statInfoFun.uncurry.uncurry âˆ˜ (fun (xÎ³ : ğ’³ Ã— â„) â†¦ ((1, xÎ³.2), ((âˆ‚Î¼/âˆ‚Î½) xÎ³.1).toReal)))
    refine stronglymeasurable_statInfoFun.measurable.comp ?_
    refine (measurable_const.prod_mk measurable_snd).prod_mk ?_
    exact ((Measure.measurable_rnDeriv Î¼ Î½).comp measurable_fst).ennreal_toReal
  have int_eq_lint : âˆ« x, âˆ« Î³, statInfoFun 1 Î³ ((âˆ‚Î¼/âˆ‚Î½) x).toReal âˆ‚curvatureMeasure hf_cvx âˆ‚Î½
      = (âˆ«â» x, âˆ«â» Î³, ENNReal.ofReal (statInfoFun 1 Î³ ((âˆ‚Î¼/âˆ‚Î½) x).toReal)
        âˆ‚curvatureMeasure hf_cvx âˆ‚Î½).toReal := by
    rw [integral_eq_lintegral_of_nonneg_ae]
    rotate_left
    Â· exact eventually_of_forall fun _ â†¦ (integral_nonneg (fun _ â†¦ statInfoFun_nonneg _ _ _))
    Â· refine (StronglyMeasurable.integral_prod_left ?_).aestronglyMeasurable
      exact (measurable_swap_iff.mpr h_meas).stronglyMeasurable
    congr with x
    rw [integral_eq_lintegral_of_nonneg_ae (eventually_of_forall fun y â†¦ statInfoFun_nonneg _ _ _)
      h_meas.of_uncurry_left.stronglyMeasurable.aestronglyMeasurable]
    refine ENNReal.ofReal_toReal <| (lintegral_ofReal_le_lintegral_nnnorm _).trans_lt ?_ |>.ne
    exact (integrable_statInfoFun 1 _).hasFiniteIntegral
  rw [int_eq_lint, lintegral_lintegral_swap h_meas.ennreal_ofReal.aemeasurable,
    integral_eq_lintegral_of_nonneg_ae]
  rotate_left
  Â· exact eventually_of_forall fun _ â†¦ (integral_nonneg (fun _ â†¦ statInfoFun_nonneg _ _ _))
  Â· exact h_meas.stronglyMeasurable.integral_prod_left.aestronglyMeasurable
  congr with Î³
  rw [integral_eq_lintegral_of_nonneg_ae (eventually_of_forall fun _ â†¦ statInfoFun_nonneg _ _ _)
    h_meas.of_uncurry_right.stronglyMeasurable.aestronglyMeasurable, ENNReal.ofReal_toReal]
  have h_lt_top := (integrable_statInfoFun_rnDeriv 1 Î³ Î¼ Î½).hasFiniteIntegral
  simp_rw [HasFiniteIntegral, lt_top_iff_ne_top] at h_lt_top
  convert h_lt_top
  rw [â† ENNReal.toReal_eq_toReal ENNReal.ofReal_ne_top ENNReal.coe_ne_top, toReal_coe_nnnorm,
    ENNReal.toReal_ofReal (statInfoFun_nonneg _ _ _),
    Real.norm_of_nonneg (statInfoFun_nonneg _ _ _)]

end StatInfoFun

end ProbabilityTheory
